
#Область УправлениеФормой

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, ИмяРеквизита)

	Если НЕ Обработано.Найти(ИмяРеквизита) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Обработано.Добавить(ИмяРеквизита);

	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;

	#Область Наборы
	
	Если ИмяРеквизита = "РеквизитыРежимаСравнения" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ГруппаПодсказкаАлгоритмаОбработки");
	КонецЕсли;

	#КонецОбласти
	
	#Область Элементы
	
	Если ИмяРеквизита = "ГруппаПодсказкаАлгоритмаОбработки" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаПодсказкаАлгоритмаОбработки", "Видимость", 
			НЕ Форма.РежимЗапроса);
	КонецЕсли;

	#КонецОбласти 
	
	#Область Команды
	
	Если ИмяРеквизита = "КомандаЗаписатьИЗакрыть" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ЗаписатьИЗакрыть", "Видимость",
			НЕ Форма.ТолькоПросмотр);
	КонецЕсли;

	#КонецОбласти 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформление(Форма, ИменаРеквизитов = "")

	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		Если ПустаяСтрока(ИменаРеквизитов) Тогда
			МассивИмен = Новый Массив;
			МассивИмен.Добавить("");
		Иначе
			МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаРеквизитов, ",");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Массив") Тогда
		МассивИмен = ИменаРеквизитов;
	Иначе
		Возврат;
	КонецЕсли;

	Обработано = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИмен Цикл
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, СокрЛП(ИмяРеквизита));
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстHTMLДокументСформирован(Элемент)
	
	БСПТ_КонсольКодаКлиент.КонсольДокументСформирован(ЭтотОбъект, "ТекстHTML", Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	БСПТ_КонсольКодаКлиент.КонсольПриНажатии(ЭтотОбъект, "ТекстHTML", Элемент, ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Заголовок			= Параметры.Заголовок;
	ЭтотОбъект.ТекстАлгоритма		= Параметры.ТекстАлгоритма;
	ЭтотОбъект.РежимЗапроса			= Параметры.РежимЗапроса;
	ЭтотОбъект.ДирективаКомпиляции	= Параметры.ДирективаКомпиляции;
	
	Если ЭтотОбъект.РежимЗапроса Тогда
		ЭтотОбъект.ДирективаКомпиляции = "НаСервере";
	КонецЕсли;
	
	БСПТ_КонсольКода.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ПараметрыПанели = БСПТ_КонсольКодаКлиентСервер.ПараметрыПанелиКонсоли();
	ПараметрыПанели.ИзменятьКомпиляцию = ПустаяСтрока(ЭтотОбъект.ДирективаКомпиляции);
	БСПТ_КонсольКода.ИнициализацияКоманднойПанели(ЭтотОбъект, "ТекстHTML", Элементы.КоманднаяПанельКонсольКода, ПараметрыПанели);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПриОткрытии(Отказ)
	
	Результат = Ждать БСПТ_КонсольКодаКлиент.ПриОткрытииАсинх(ЭтотОбъект, Отказ);
	
	Если НЕ Результат.Выполнено Тогда
		ВызватьИсключение НСтр("ru='Ошибка при открытии формы консоли кода: '") + Результат.ТекстОшибки;
	КонецЕсли;

	Ждать БСПТ_КонсольКодаКлиент.ИнициализацияПоляАсинх(ЭтотОбъект, "ТекстHTML");
	
	УстановитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение,
		Отказ,
		ЗавершениеРаботы);
		
	Если НЕ Отказ Тогда
		БСПТ_КонсольКодаКлиент.ПередЗакрытиемАсинх(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЗаписатьИЗакрытьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура КомандаКонсолиКода(Команда)
	
	БСПТ_КонсольКодаКлиент.ВыполнитьКомандуКонсолиКода(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте 
Процедура ПослеИнициализациПоляКонсоли(знач ИмяРеквизита, знач ДопПараметры) Экспорт

	Если ИмяРеквизита <> "ТекстHTML" Тогда
		Возврат;
	КонецЕсли;
	
	Консоль = БСПТ_КонсольКодаКлиент.ЭтаКонсоль(ЭтотОбъект, ИмяРеквизита);
	
	БСПТ_КонсольКодаКлиент.УстановитьДирективуКомпиляции(Консоль, ЭтотОбъект.ДирективаКомпиляции, Ложь);
	БСПТ_КонсольКодаКлиент.ОбнулитьМетаданные(Консоль); // выполняется в методе УстановитьДирективуКомпиляции
	БСПТ_КонсольКодаКлиент.РежимЗапроса(Консоль, ЭтотОбъект.РежимЗапроса);
	БСПТ_КонсольКодаКлиент.УстановитьТекст(Консоль, ЭтотОбъект.ТекстАлгоритма);
	БСПТ_КонсольКодаКлиент.УстановитьОригинальныйТекст(Консоль, ЭтотОбъект.ТекстАлгоритма);
	
	БСПТ_КонсольКодаКлиент.ПодсвечиватьЗапросы(Консоль, ЭтотОбъект.РежимЗапроса);
	
	БСПТ_КонсольКодаКлиент.ЗагрузитьПользовательскиеОбъекты(Консоль, ЭтотОбъект.Параметры.ПользовательскиеОбъекты);
		
	//БСПТ_КонсольКодаКлиент.ДоступностьСравнения(Консоль, Ложь);
	//БСПТ_КонсольКодаКлиент.БыстрыеПодсказки(Консоль, Ложь);
	//БСПТ_КонсольКодаКлиент.КартаКода(Консоль, Ложь);
	
КонецПроцедуры

&НаКлиенте 
Процедура ПриИзмененииПоляКонсоли(знач Консоль) Экспорт
	
	//ЗапросТекст = БСПТ_КонсольКодаКлиент.ПолучитьТекст(Консоль);
	//
	//ЗаписатьТекстЗапроса(ЗапросТекст);
	//
	//ЭтотОбъект.ТекстЗапроса.УстановитьТекст(ЗапросТекст);
	
КонецПроцедуры

&НаКлиенте 
Процедура ПередЗакрытиемЗавершение(Результат, ДопПараметры) Экспорт
	ЭтотОбъект.Модифицированность = Ложь;
	ЗаписатьИЗакрытьНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура ЗаписатьИЗакрытьНаКлиенте()
	Если ЭтотОбъект.Модифицированность Тогда
		Консоль = БСПТ_КонсольКодаКлиент.ЭтаКонсоль(ЭтотОбъект, "ТекстHTML");
		ТекстКода = БСПТ_КонсольКодаКлиент.ПолучитьТекст(Консоль);
	Иначе 
		ТекстКода = Неопределено;
	КонецЕсли;
	
	Если ЭтотОбъект.Модифицированность Тогда
		ЭтотОбъект.Модифицированность = Ложь;
			
		Если ЭтотОбъект.Параметры.РежимВыбора Тогда
			ЭтотОбъект.ЗакрыватьПриВыборе = Ложь;
			ЭтотОбъект.ОповеститьОВыборе(ТекстКода);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтотОбъект.Открыта() Тогда
		ЭтотОбъект.Закрыть(ТекстКода);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ЗагрузитьВременныеТаблицыВПользовательскиеОбъекты(Консоль)
	
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Раздел
#КонецОбласти



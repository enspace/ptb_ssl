#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ИспользоватьПоУмолчанию Тогда
		ОсновнаяБазаЗнаний = Справочники.БазыЗнанийConfluence.ОсновнаяБазаЗнаний();
		Если ЗначениеЗаполнено(ОсновнаяБазаЗнаний) И ОсновнаяБазаЗнаний <> ЭтотОбъект.Ссылка Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru='Основная база знаний уже установлена (%1).'"), Строка(ОсновнаяБазаЗнаний)),,
				"Объект.ИспользоватьПоУмолчанию",,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	УдаляемыеРеквизиты = Новый Массив;
	
	Если НЕ ЭтотОбъект.ЕдинаяАвторизация Тогда
		УдаляемыеРеквизиты.Добавить("ПользовательАвторизации");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, УдаляемыеРеквизиты);
КонецПроцедуры

#КонецОбласти

#КонецЕсли 
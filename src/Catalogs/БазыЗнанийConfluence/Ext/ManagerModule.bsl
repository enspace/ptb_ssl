
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает количество баз знаний не помеченных на удаление
//
// Возвращаемое значение:
//   Число
// 
Функция КоличествоБазЗнаний() Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(БазыЗнанийConfluence.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.БазыЗнанийConfluence КАК БазыЗнанийConfluence
	|ГДЕ
	|	НЕ БазыЗнанийConfluence.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат ?(НЕ ЗначениеЗаполнено(Выборка.Количество), 0, Выборка.Количество);
	Иначе 
		Возврат 0;
	КонецЕсли;  
КонецФункции

// Возвращает основную базу знаний. Основной считается база без пометки
// на удаление и с установленным признаком "ИспользоватьПоУмолчанию"
//
// Возвращаемое значение:
//   СправочникСсылка.БазыЗнанийConfluence
// 
Функция ОсновнаяБазаЗнаний() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	БазыЗнанийConfluence.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.БазыЗнанийConfluence КАК БазыЗнанийConfluence
	|ГДЕ
	|	НЕ БазыЗнанийConfluence.ПометкаУдаления
	|	И БазыЗнанийConfluence.ИспользоватьПоУмолчанию
	|
	|УПОРЯДОЧИТЬ ПО
	|	БазыЗнанийConfluence.Код";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

// Возвращает структуру настройки подключения к базе знаний
//
// Параметры:
//	БазаЗнанийСсылка - СправочникСсылка.БазыЗнанийConfluence
//
// Возвращаемое значение:
//   Структура
//		Наименование			- Строка - представление базы знаний
//		АдресСервера			- Строка - полный адрес сервера базы знаний (https://name.atlassian.net)
//		ЗащищенноеСоединение	- Булево - использование защищенного соединения с базой знаний (HTTPS)
//		СерверHTTP				- Строка - адрес сервера без способа соединения (name.atlassian.net)
//		АдресСервераWiki		- Строка - адрес сервера с указанием wiki на конце (https://name.atlassian.ne/wiki)
// 
Функция НастройкиБазыЗнаний(знач БазаЗнанийСсылка) Экспорт 
	
	РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БазаЗнанийСсылка, "Наименование, АдресСервера");
	
	Схема = "";
	Адрес = "";
	
	Позиция = СтрНайти(РеквизитыСсылки.АдресСервера, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(РеквизитыСсылки.АдресСервера, Позиция - 1));
		Адрес = Сред(РеквизитыСсылки.АдресСервера, Позиция + 3);
	КонецЕсли;
	Адрес = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(Адрес, "/", "Справа");
	
	АдресWiki = РеквизитыСсылки.АдресСервера + ?(СтрЗаканчиваетсяНа(РеквизитыСсылки.АдресСервера, "/"), "", "/") + "wiki";
	
	РеквизитыСсылки.Вставить("ЗащищенноеСоединение"	, (Схема = "https"));
	РеквизитыСсылки.Вставить("СерверHTTP"			, Адрес);
	РеквизитыСсылки.Вставить("АдресСервераWiki"		, АдресWiki); 
	 
	Возврат РеквизитыСсылки; 
	
КонецФункции

#КонецОбласти

#КонецЕсли 
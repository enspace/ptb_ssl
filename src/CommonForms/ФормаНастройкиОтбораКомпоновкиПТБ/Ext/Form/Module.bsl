
#Область ОбработчикиСобытийЭлементовТаблицыФормы_КомпоновщикНастроекНастройкиОтбор

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ОткрытьФормуВыбораДоступногоПоля(Элемент, Отказ, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиОтборЛевоеЗначение" Тогда
	
		ОткрытьФормуВыбораДоступногоПоля(Элемент, Отказ);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборЛевоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Отказ = Ложь;
	ОткрытьФормуВыбораДоступногоПоля(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборЛевоеЗначениеПриИзменении(Элемент)
	
	ТекущаяСтрока	= Элементы.КомпоновщикНастроекНастройкиОтбор.ТекущаяСтрока;
	ЭлементОтбора	= ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	ТекущееПоле		= Строка(ЭлементОтбора.ЛевоеЗначение);
	
	Если ТипЗнч(Параметры.ЗапрещенныеПоля) = Тип("Массив") И Параметры.ЗапрещенныеПоля.Найти(ТекущееПоле) <> Неопределено Тогда
		ЭлементОтбора.ЛевоеЗначение = Неопределено;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(НСтр("ru='Поле ""%1"" запрещено к выбору в отборе.'"), ТекущееПоле));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если НЕ ТипЗнч(Параметры.КомпоновщикНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не верно переданы параметры отбора. Попробуйте еще раз или обратитесь к разработчику.'"),,,, Отказ); 
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.АдресХранилища		= Параметры.АдресХранилища;
	ЭтотОбъект.КомпоновщикНастроек	= Параметры.КомпоновщикНастроек;
	
	Если НЕ ПустаяСтрока(Параметры.Заголовок) Тогда
		ЭтотОбъект.Заголовок		= Параметры.Заголовок;
		ЭтотОбъект.АвтоЗаголовок	= Ложь;
	КонецЕсли;
	
	ОграничитьДоступностьПолейНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)  
	КлючОбъектаПоВладельцу = ПомощникЗаполненияПТБКлиентСервер.КлючОбъектаНастроекПоФормеВладельца(ЭтотОбъект.ВладелецФормы); 
	ЭтотОбъект.КлючОбъекта = ?(КлючОбъектаПоВладельцу = Неопределено, ЭтотОбъект.ИмяФормы, КлючОбъектаПоВладельцу);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемВопросЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(ОписаниеОповещения, Отказ, ЗавершениеРаботы);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЭтотОбъект.Модифицированность = Ложь;
	ЭтотОбъект.Закрыть(ЭтотОбъект.КомпоновщикНастроек);
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьНастройку(Команда)  
	
	ПредставлениеОтбора = Строка(ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЭтоСохранение"			, Истина);
	ПараметрыОткрытия.Вставить("КлючОбъекта"			, ЭтотОбъект.КлючОбъекта);
	ПараметрыОткрытия.Вставить("ИмяСохраняемыхНастроек"	, ПредставлениеОтбора);

	ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьНастройкуЗавершение", ЭтотОбъект);	
	
	ОткрытьФорму("ОбщаяФорма.ФормаУправленияНастройкамиОтборовПТБ", 
		ПараметрыОткрытия,,,,, 
		ОписаниеОповещения, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
		
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьНастройку(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КлючОбъекта", ЭтотОбъект.КлючОбъекта);

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьНастройкуЗавершение", ЭтотОбъект);	
	
	ОткрытьФорму("ОбщаяФорма.ФормаУправленияНастройкамиОтборовПТБ", 
		ПараметрыОткрытия,,,,, 
		ОписаниеОповещения, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ЗагрузитьНастройкуЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	ОтборКомпоновкиДанных = ОбщегоНазначенияПТБКлиентСервер.СвойствоСтруктуры(РезультатЗакрытия, "ОтборКомпоновкиДанных", Неопределено); 
	
	КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();

	Если ТипЗнч(ОтборКомпоновкиДанных) = Тип("ОтборКомпоновкиДанных") Тогда
		ОбщегоНазначенияПТБКлиентСервер.ЗаполнитьОтборКомпоновкиДанныхПоИсточнику(
			КомпоновщикНастроек.Настройки.Отбор,
			ОтборКомпоновкиДанных);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкуЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	КлючНастроек = РезультатЗакрытия.КлючНастроек;
	Представление = РезультатЗакрытия.ИмяСохраняемыхНастроек;

	СохранитьНастройкуНаСервере(КлючНастроек, Представление);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемВопросЗавершение(РезультатВопроса, ПараметрыВыполнения) Экспорт
	ЭтотОбъект.Модифицированность = Ложь;
	ЭтотОбъект.Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзмененияЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("ДобавлятьСтроку") И ДополнительныеПараметры.ДобавлятьСтроку Тогда
		ЭлементОбъект = ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Иначе 
		ТекущаяСтрока = Элементы.КомпоновщикНастроекНастройкиОтбор.ТекущаяСтрока;
		ЭлементОбъект = ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
	КонецЕсли;
	
	ЭлементОбъект.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(РезультатЗакрытия);
	Элементы.КомпоновщикНастроекНастройкиОтбор.ТекущаяСтрока = ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.ПолучитьИдентификаторПоОбъекту(ЭлементОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура ОткрытьФормуВыбораДоступногоПоля(Элемент, Отказ, знач ДобавлятьСтроку = Ложь)
	
	Отказ = Истина;
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ДобавлятьСтроку Тогда
		ТекущаяСтрока	= Элементы.КомпоновщикНастроекНастройкиОтбор.ТекущаяСтрока;
		ЭлементОтбора	= ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
		ТекущееПоле		= Строка(ЭлементОтбора.ЛевоеЗначение);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресХранилища"		, ЭтотОбъект.АдресХранилища);
	ПараметрыФормы.Вставить("КомпоновщикНастроек"	, ЭтотОбъект.КомпоновщикНастроек);
	ПараметрыФормы.Вставить("ЗапрещенныеПоля"		, Параметры.ЗапрещенныеПоля);
	ПараметрыФормы.Вставить("ТекущееПоле"			, ТекущееПоле);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ДобавлятьСтроку", ДобавлятьСтроку);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОтборыПередНачаломИзмененияЗавершение", ЭтотОбъект, ДопПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораПоляОтбораКомпоновкиПТБ",
		ПараметрыФормы, Элемент,,,,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере 
Процедура ОграничитьДоступностьПолейНаСервере()
	
	ПолеОграничения = Элементы.КомпоновщикНастроекНастройкиОтборДоступныеПоляОтбора.ОграниченияИспользования.Добавить();
	ПолеОграничения.Поле		= Новый ПолеКомпоновкиДанных("ПараметрыДанных");
	ПолеОграничения.Доступность	= Ложь;
	
	Если ТипЗнч(Параметры.ЗапрещенныеПоля) = Тип("Массив") Тогда
		Для Каждого ИмяПоля Из Параметры.ЗапрещенныеПоля Цикл
			ПолеОграничения = Элементы.КомпоновщикНастроекНастройкиОтборДоступныеПоляОтбора.ОграниченияИспользования.Добавить();
			ПолеОграничения.Поле		= Новый ПолеКомпоновкиДанных(ИмяПоля);
			ПолеОграничения.Доступность	= Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуНаСервере(КлючНастроек, Представление)
	
	ИмяПользователя = ИмяПользователя();
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ОтборXML = ПомощникЗаполненияПТБ.ПолучитьСтрокуДляСохраненияОтбора(ЭтотОбъект, "КомпоновщикНастроек");
	ПредставлениеОтбора = Строка(ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор);
	
	Настройки = Новый Соответствие;
	Настройки.Вставить("ОтборXML"		, ОтборXML);
	Настройки.Вставить("Пользователь"	, ТекущийПользователь);
	Настройки.Вставить("Владелец"		, ЭтотОбъект.КлючОбъекта);
	Настройки.Вставить("Дата"			, ТекущаяДатаСеанса());
	Настройки.Вставить("Представление"	, ПредставлениеОтбора);
	
    КлючНастроек = ?(КлючНастроек = Неопределено, Строка(Новый УникальныйИдентификатор), КлючНастроек);

	ОписаниеНастроек = Новый ОписаниеНастроек;
	ОписаниеНастроек.Пользователь 	= ИмяПользователя;
	ОписаниеНастроек.Представление	= Представление;
	
	ОписаниеНастроек.ДополнительныеСвойства.Вставить("Пользователь"	, ТекущийПользователь);

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ЭтотОбъект.КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, ИмяПользователя);

КонецПроцедуры

#КонецОбласти

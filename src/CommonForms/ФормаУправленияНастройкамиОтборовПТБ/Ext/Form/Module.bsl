
#Область УправлениеФормой

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, ИмяРеквизита)

	Если НЕ Обработано.Найти(ИмяРеквизита) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Обработано.Добавить(ИмяРеквизита);

	Элементы = Форма.Элементы;
	
	Если Форма.ЭтоСохранение Тогда
		ЗаголовокКоманды = "Сохранить";
		КартинкаКоманды = БиблиотекаКартинок.СохранитьНастройкиОтчета;
	Иначе
		ЗаголовокКоманды = "Выбрать";
		КартинкаКоманды = БиблиотекаКартинок.ЗагрузитьНастройкиОтчета;	
	КонецЕсли;
	
	#Область Элементы
	
	Если ИмяРеквизита = "ИмяСохраняемыхНастроек" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИмяСохраняемыхНастроек", "Видимость", Форма.ЭтоСохранение);
	КонецЕсли;

	Если ИмяРеквизита = "НастройкиВсехПользователей" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"НастройкиВсехПользователей", "Видимость", НЕ Форма.ЭтоСохранение);
	КонецЕсли;
		
	#КонецОбласти 
	
	#Область ТабЧасть_Имя

	Если ИмяРеквизита = "СписокНастроекПользователь" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СписокНастроекПользователь", "Видимость", Форма.НастройкиВсехПользователей);
	КонецЕсли;
	Если ИмяРеквизита = "СписокНастроекКлючОбъекта" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СписокНастроекКлючОбъекта", "Видимость", Форма.ПравоАдминистрированиеДанных);
	КонецЕсли;
	Если ИмяРеквизита = "СписокНастроекКлючНастроек" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СписокНастроекКлючНастроек", "Видимость", Форма.ПравоАдминистрированиеДанных);
	КонецЕсли;

	#КонецОбласти
	
	#Область Команды
	
	Если ИмяРеквизита = "Выбрать" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"Выбрать", "Заголовок", ЗаголовокКоманды);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"Выбрать", "Картинка", КартинкаКоманды);
	КонецЕсли;

	#КонецОбласти 

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформление(Форма, ИменаРеквизитов = "") Экспорт

	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		Если ПустаяСтрока(ИменаРеквизитов) Тогда
			МассивИмен = Новый Массив;
			МассивИмен.Добавить("");
		Иначе
			МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаРеквизитов, ",");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Массив") Тогда
		МассивИмен = ОбщегоНазначенияПТБКлиентСервер.СкопироватьРекурсивно(ИменаРеквизитов);
	Иначе
		Возврат;
	КонецЕсли;

	Обработано = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИмен Цикл
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, СокрЛП(ИмяРеквизита));
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Неопределено); 
КонецПроцедуры

&НаКлиенте
Процедура ТолькоСобственныеНастройкиПриИзменении(Элемент)
	ЗаполнитьСписокНастроекНаСервере(ЭтотОбъект.КлючОбъекта); 
	УстановитьУсловноеОформление(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("КлючОбъекта") Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	ПравоАдминистрированиеДанных = ПравоДоступа("АдминистрированиеДанных", Метаданные);

	Параметры.Свойство("КлючОбъекта"			, ЭтотОбъект.КлючОбъекта);
	Параметры.Свойство("ЭтоСохранение"			, ЭтотОбъект.ЭтоСохранение);
	Параметры.Свойство("ИмяСохраняемыхНастроек"	, ЭтотОбъект.ИмяСохраняемыхНастроек);

	ЗаполнитьСписокНастроекНаСервере(ЭтотОбъект.КлючОбъекта); 
	
	ЭтотОбъект.Заголовок = ?(ЭтотОбъект.ЭтоСохранение, "Сохранение настройки отбора", "Выбор настройки отбора");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьУсловноеОформление(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если ЭтотОбъект.ЭтоСохранение Тогда
		ПроверяемыеРеквизиты.Добавить("ИмяСохраняемыхНастроек");	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ВыбратьНастройку()
	ТекущиеДанные = Элементы.СписокНастроек.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("КлючНастроек", ТекущиеДанные.КлючНастроек);
	ПараметрыЗакрытия.Вставить("Пользователь", ТекущиеДанные.Пользователь);
	ПараметрыЗакрытия.Вставить("ОтборКомпоновкиДанных", ОтборКомпоновкиНаСервере(ТекущиеДанные.КлючНастроек, ТекущиеДанные.Пользователь));

	Закрыть(ПараметрыЗакрытия);
КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьНастройку(знач ЭтоВыборВСписке) 
	
	НовоеПредставлениеНастройки = "";
	Если ЭтоВыборВСписке Тогда
		НайденнаяНастройка = Элементы.СписокНастроек.ТекущиеДанные;
		Если НайденнаяНастройка = Неопределено Тогда
			Возврат;
		КонецЕсли;                                                     
		НовоеПредставлениеНастройки = НайденнаяНастройка.Представление; 
		ЭтотОбъект.ИмяСохраняемыхНастроек = НовоеПредставлениеНастройки;
	Иначе
		Если НЕ ЭтотОбъект.ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		
		НайденнаяНастройка = НайденнаяНастройка(ЭтотОбъект.ИмяСохраняемыхНастроек); 
		НовоеПредставлениеНастройки = ЭтотОбъект.ИмяСохраняемыхНастроек;
	КонецЕсли;
	
	Если НайденнаяНастройка = Неопределено Тогда
		КлючНастроек = Неопределено;
		Ответ = КодВозвратаДиалога.Да;
	Иначе
		КлючНастроек = НайденнаяНастройка.КлючНастроек;
		
		Ответ = Ждать ВопросАсинх(СтрШаблон("Заменить ранее сохраненные настройки ""%1""", НовоеПредставлениеНастройки),
			РежимДиалогаВопрос.ДаНет);		 
	КонецЕсли;
		
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("КлючНастроек"			, КлючНастроек);
	ПараметрыЗакрытия.Вставить("ИмяСохраняемыхНастроек"	, ЭтотОбъект.ИмяСохраняемыхНастроек);
	Закрыть(ПараметрыЗакрытия);       
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Асинх Процедура Удалить(Команда)
	ТекущиеДанные = Элементы.СписокНастроек.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ответ = Ждать ВопросАсинх("Вы действительно хотите удалить настройку?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьНаСервере(ТекущиеДанные.КлючОбъекта, ТекущиеДанные.КлючНастроек);
	КонецЕсли;        
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов
  
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура Выбрать(Команда)
	Если ЭтотОбъект.ЭтоСохранение Тогда
		ЭтоВыборВСписке = (Команда = Неопределено);
		СохранитьНастройку(ЭтоВыборВСписке);	
	Иначе
		ВыбратьНастройку();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция НайденнаяНастройка(знач Представление) 
	
	Отбор = Новый Структура;
	Отбор.Вставить("Представление"	, Представление);
	Отбор.Вставить("Пользователь"	, ИмяПользователя());
	
	НайденныеСтроки = ЭтотОбъект.СписокНастроек.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат НайденныеСтроки[0];
	КонецЕсли;		
		
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокНастроекНаСервере(знач КлючОбъекта)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокНастроек.Очистить();
	
	Структура = Новый Структура;
	Структура.Вставить("КлючОбъекта", КлючОбъекта); 
	Если ЭтотОбъект.НастройкиВсехПользователей = Ложь Тогда
		Структура.Вставить("Пользователь", ИмяПользователя());
	КонецЕсли;
	
	Выборка = ХранилищеОбщихНастроек.Выбрать(Структура);
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СписокНастроек.Добавить(), Выборка);
	КонецЦикла;        
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервере(знач КлючОбъекта, знач КлючНастроек)
	ОбщегоНазначения.ХранилищеОбщихНастроекУдалить(КлючОбъекта, КлючНастроек, ИмяПользователя());
	ЗаполнитьСписокНастроекНаСервере(КлючОбъекта);
КонецПроцедуры

&НаСервере
Функция ОтборКомпоновкиНаСервере(знач КлючНастроек, знач Пользователь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СохраненныеНастройки = Неопределено;
    Попытка
		СохраненныеНастройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ЭтотОбъект.КлючОбъекта, КлючНастроек,,,Пользователь);
    Исключение
        ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Нет прав на восстановление настроек.'"));
		Возврат "";
    КонецПопытки;
	
	Если СохраненныеНастройки = Неопределено Тогда
		Возврат "";	
	КонецЕсли;  
	
	ОтборXML = ОбщегоНазначенияПТБКлиентСервер.СвойствоСоответствия(СохраненныеНастройки, "ОтборXML", Неопределено);
	Если ОтборXML = Неопределено Тогда
		Возврат  "";
	КонецЕсли;	

	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ОтборXML);

	Возврат СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	
КонецФункции

#КонецОбласти




#Область УправлениеФормой

&НаСервере
Процедура ИнициализацияФормы()
	
	// ПомощникЗаполненияПТБ.Начало
	ПомощникЗаполненияПТБ.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект,
		"ОбъектыКомпоновщикНастроек",
		Документы.ТестовыйДокументПомощникЗаполненияПТБ.ПолучитьМакет("ПомощникЗаполненияПТБ_СКД"),
		Объект.ОтборXML);
	
	ПомощникЗаполненияПТБ.ДобавитьПолеНастройкиОтбора(ЭтотОбъект, "ОбъектыКомпоновщикНастроек", "ГруппаНастройкиОтбораКомпоновщика");
	// ПомощникЗаполненияПТБ.Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ИнициализацияФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ИнициализацияФормы();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ПомощникЗаполненияПТБ.Начало
	ТекущийОбъект.ОтборXML = ПомощникЗаполненияПТБ.ПолучитьСтрокуДляСохраненияОтбора(ЭтотОбъект, "ОбъектыКомпоновщикНастроек");
	// ПомощникЗаполненияПТБ.Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьОбъекты(Команда)
	
	ЗаполнитьОбъектыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура ЗаполнитьОбъектыНаСервере()
	
	ВыбранныеПоля = Новый Массив;
	ВыбранныеПоля.Добавить("ОбъектМетаданных");
	
	НастройкиОтбора = Новый ОтборКомпоновкиДанных;
	//ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НастройкиОтбора,
	//	"Банк.Город", "Г. УФА", ВидСравненияКомпоновкиДанных.Равно);
	
	НастройкиПорядок = Новый Соответствие;
	НастройкиПорядок.Вставить("ОбъектМетаданных.Наименование", "ВОЗР");
	
	// ПомощникЗаполненияПТБ.Начало
	ТаблицаДанных = ПомощникЗаполненияПТБ.ПолучитьТаблицуЗаполнения(ЭтотОбъект,
		"ОбъектыКомпоновщикНастроек",
		ВыбранныеПоля,,
		НастройкиОтбора,
		НастройкиПорядок);
	// ПомощникЗаполненияПТБ.Конец
		
	Объект.ОбъектыМетаданных.Очистить();
	Объект.ОбъектыМетаданных.Загрузить(ТаблицаДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ПТБ_БСП

// ПомощникЗаполненияПТБ.Начало
&НаКлиенте
Процедура Подключаемый_ПомощникЗаполненияОтборНажатие(Элемент, СтандартнаяОбработка)
	
	ЗапрещенныеПоля = Новый Массив;
	ЗапрещенныеПоля.Добавить("ОбъектМетаданных.ПорядокКоллекции");
	ЗапрещенныеПоля.Добавить("ОбъектМетаданных.КлючОбъектаМетаданных");
	
	ПомощникЗаполненияПТБКлиент.ПомощникЗаполненияОтборНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка, ЗапрещенныеПоля);
	
КонецПроцедуры
// ПомощникЗаполненияПТБ.Конец

#КонецОбласти



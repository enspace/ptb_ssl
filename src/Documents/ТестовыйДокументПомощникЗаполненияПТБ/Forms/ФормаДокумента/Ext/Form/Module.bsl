
#Область УправлениеФормой

&НаСервере
Процедура ИнициализацияФормы()
	
	// ПомощникЗаполненияПТБ.Начало
	ПомощникЗаполненияПТБ.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект,
		"БанкиКомпоновщикНастроек",
		Документы.ТестовыйДокументПомощникЗаполненияПТБ.ПолучитьМакет("ПомощникЗаполненияПТБ_СКД"),
		Объект.ОтборXML);
	
	ПомощникЗаполненияПТБ.ДобавитьПолеНастройкиОтбора(ЭтотОбъект, "БанкиКомпоновщикНастроек", "ГруппаНастройкиОтбораБанки");
	// ПомощникЗаполненияПТБ.Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ИнициализацияФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ИнициализацияФормы();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ПомощникЗаполненияПТБ.Начало
	ТекущийОбъект.ОтборXML = ПомощникЗаполненияПТБ.ПолучитьСтрокуДляСохраненияОтбора(ЭтотОбъект, "БанкиКомпоновщикНастроек");
	// ПомощникЗаполненияПТБ.Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьБанки(Команда)
	
	ЗаполнитьБанкиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура ЗаполнитьБанкиНаСервере()
	
	ВыбранныеПоля = Новый Массив;
	ВыбранныеПоля.Добавить("Банк");
	
	НастройкиОтбора = Новый ОтборКомпоновкиДанных;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НастройкиОтбора,
		"Банк.Город", "Г. УФА", ВидСравненияКомпоновкиДанных.Равно);
	
	НастройкиПорядок = Новый Соответствие;
	НастройкиПорядок.Вставить("Банк.Наименование", "ВОЗР");
	
	// ПомощникЗаполненияПТБ.Начало
	ТаблицаДанных = ПомощникЗаполненияПТБ.ПолучитьТаблицуЗаполнения(ЭтотОбъект,
		"БанкиКомпоновщикНастроек",
		ВыбранныеПоля,,
		НастройкиОтбора,
		НастройкиПорядок);
	// ПомощникЗаполненияПТБ.Конец
		
	Объект.Банки.Очистить();
	Объект.Банки.Загрузить(ТаблицаДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ПТБ_БСП

// ПомощникЗаполненияПТБ.Начало
&НаКлиенте
Процедура Подключаемый_ПомощникЗаполненияОтборНажатие(Элемент, СтандартнаяОбработка)
	
	ЗапрещенныеПоля = Новый Массив;
	ЗапрещенныеПоля.Добавить("Банк.Адрес");
	ЗапрещенныеПоля.Добавить("Банк.ИмяПредопределенныхДанных");
	
	ПомощникЗаполненияПТБКлиент.ПомощникЗаполненияОтборНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка, ЗапрещенныеПоля);
	
КонецПроцедуры
// ПомощникЗаполненияПТБ.Конец

#КонецОбласти



#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработкаПроведения

Процедура ПодготовитьПараметрыПроведения(ДокументСсылка, ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	// Формирование ПараметрыПроведения.Реквизиты
	ИнициализироватьРеквизитыДокумента(Запрос, ДополнительныеСвойства, Отказ);
	
	// Формирование таблиц данных запросов
	ТекстЗапросаТабличныеЧасти(Запрос, ДополнительныеСвойства);
	ТекстЗапросаРегистрОстатки(Запрос, ДополнительныеСвойства);
	
	ПроведениеСерверПТБ.ВыполнитьЗапросДанных(Запрос, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеПроверки(ДополнительныеСвойства, Отказ) Экспорт
	
КонецПроцедуры

Процедура СформироватьДвиженияПоРегистрам(Движения, ДополнительныеСвойства, Отказ) Экспорт
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияРегистрОстатки(Движения, ДополнительныеСвойства, Отказ);
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля(Движения, ДополнительныеСвойства, Отказ) Экспорт
	Массив = ДополнительныеСвойства.ОбщиеДанные.РегистрыДляКонтроля;
	Массив.Добавить(Движения.РегистрОстатки);
КонецПроцедуры

Процедура СообщитьКонтрольРезультатовПроведения(ДополнительныеСвойства, Отказ) Экспорт
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	СообщитьКонтрольРегистрОстатки(ДополнительныеСвойства, Отказ);
КонецПроцедуры

#Область ПодготовитьПараметрыПроведения

Процедура ИнициализироватьРеквизитыДокумента(Запрос, ДополнительныеСвойства, Отказ)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""ТестовыйДокументПроведение"" КАК ВидДокумента,
	|	ТестовыйДокументПроведение.Ссылка КАК Ссылка,
	|	ТестовыйДокументПроведение.Номер КАК Номер,
	|	ТестовыйДокументПроведение.Дата КАК Дата,
	|	ТестовыйДокументПроведение.Организация КАК Организация,
	|	ТестовыйДокументПроведение.ЭтоРасход КАК ЭтоРасход,
	|	ТестовыйДокументПроведение.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.ТестовыйДокументПроведение КАК ТестовыйДокументПроведение
	|ГДЕ
	|	ТестовыйДокументПроведение.Ссылка = &Ссылка";
	
	ПроведениеСерверПТБ.ДобавитьТаблицуЗапроса("Реквизиты", ДополнительныеСвойства);
	ПроведениеСерверПТБ.ДобавитьТекстЗапроса(ТекстЗапроса, Запрос);
	
	ПроведениеСерверПТБ.ВыполнитьЗапросДанныхРеквизитов(Запрос, ДополнительныеСвойства);
	
	// Дополнение параметров
	Реквизиты = ДополнительныеСвойства.ПараметрыПроведения.Реквизиты;
	Запрос.УстановитьПараметр("НачалоПериода", Реквизиты.МоментВремени);
	
КонецПроцедуры

Процедура ТекстЗапросаТабличныеЧасти(Запрос, ДополнительныеСвойства)
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.Пользователь КАК Пользователь,
	|	ТабличнаяЧасть.Количество КАК Количество
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	Документ.ТестовыйДокументПроведение.ТабличнаяЧасть КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка";
	
	ПроведениеСерверПТБ.ДобавитьТаблицуЗапроса("ВТ_ТабличнаяЧасть", ДополнительныеСвойства);
	ПроведениеСерверПТБ.ДобавитьТекстЗапроса(ТекстЗапроса, Запрос);
	
КонецПроцедуры

Процедура ТекстЗапросаРегистрОстатки(Запрос, ДополнительныеСвойства)
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	МИНИМУМ(ТабличнаяЧасть.НомерСтроки) КАК НомерСтрокиТЧ,
	|	ВЫБОР
	|		КОГДА &ЭтоРасход
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТабличнаяЧасть.Пользователь КАК Пользователь,
	|	СУММА(ТабличнаяЧасть.Количество) КАК Количество
	|ИЗ
	|	ТабличнаяЧасть КАК ТабличнаяЧасть
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Пользователь";
	
	ПроведениеСерверПТБ.ДобавитьТаблицуЗапроса("РегистрОстатки", ДополнительныеСвойства);
	ПроведениеСерверПТБ.ДобавитьТекстЗапроса(ТекстЗапроса, Запрос);
	
КонецПроцедуры

#КонецОбласти

#Область СформироватьДвиженияПоРегистрам

Процедура СформироватьДвиженияРегистрОстатки(Движения, ДополнительныеСвойства, Отказ) Экспорт
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = ДополнительныеСвойства.ПараметрыПроведения;
	
	Движения.РегистрОстатки.Записывать = Истина;
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.РегистрОстатки Цикл
		ДвижениеРегистра = Движения.РегистрОстатки.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеРегистра, СтрокаТаблицы);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 

#Область СообщитьКонтрольРезультатовПроведения

Процедура СообщитьКонтрольРегистрОстатки(ДополнительныеСвойства, Отказ) Экспорт
	РезультатКонтроля = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства.РезультатКонтроля,
		"РегистрОстатки", Неопределено);
	
	Если НЕ ТипЗнч(РезультатКонтроля) = Тип("ТаблицаЗначений") ИЛИ РезультатКонтроля.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = ДополнительныеСвойства.ОбщиеДанные.Ссылка;;
	
	Для Каждого СтрокаКонтроля Из РезультатКонтроля Цикл
		ОтборСтрок = Новый Структура("Пользователь");
		ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаКонтроля);
		
		ПутьКРеквизиту = ПроведениеСерверПТБ.ПутьКТабличнойЧасти(ДополнительныеСвойства,
			"РегистрОстатки", ОтборСтрок, "Объект.ТабличнаяЧасть.Количество");
		
		Если СтрокаКонтроля.Количество < 0 Тогда
			СтрПользователь = Строка(СтрокаКонтроля.Пользователь);
			
			ТекстСообщения = СтрШаблон(НСтр("ru='Недостаточно количества по пользователю ""%1"" (недостаток: %2 %3)'"),
				СтрПользователь,
				Формат(-СтрокаКонтроля.Количество, "ЧДЦ=3; ЧН=0.000; ЧГ="),
				НСтр("ru='ед.'"));
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ДокументСсылка, ПутьКРеквизиту,, Отказ);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#КонецЕсли 
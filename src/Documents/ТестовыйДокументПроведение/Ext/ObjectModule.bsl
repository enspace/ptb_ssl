#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьПоОбъектуОснованию(ОбъектОснование) Экспорт
	ТипОбъектОснование = ТипЗнч(ОбъектОснование);
	
	//Если ТипОбъектОснование = Тип("ДокументСсылка.ЗаявкаНаОплату") Тогда
	//	ЗаполнитьПоДокументуЗаявкаНаОплату(ДокОснование);
	//Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Заполнение по объекту ""%1"" невозможно. Обратитесь к администратору'"),
			Строка(ОбъектОснование));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения); 
	//КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведенияБезКонтроля(Отказ)
	
	// Предварительная подготовка параметров
	ПроведениеСерверПТБ.ИнициализацияДополнительныхСвойствДляПроведения(ЭтотОбъект, ЭтотОбъект.ДополнительныеСвойства);
	
	// Проверим наличие ручной корректировки по документу
	ПроведениеСерверПТБ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, Отказ);
	
	// Запись наборов
	ПроведениеСерверПТБ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Очистка дополнительных свойств
	ПроведениеСерверПТБ.ОчиститьДополнительныеСвойства(ЭтотОбъект.ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведенияСКонтролем(Отказ)
	
	МодульМенеджера = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ЭтотОбъект.Ссылка);

	// Предварительная подготовка параметров
	ПроведениеСерверПТБ.ИнициализацияДополнительныхСвойствДляПроведения(ЭтотОбъект.Ссылка, ЭтотОбъект.ДополнительныеСвойства);
	
	// Проверим наличие ручной корректировки по документу
	ПроведениеСерверПТБ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, Отказ);
	
	// Добавляет регистры для выполнения контроля движений
	МодульМенеджера.СформироватьСписокРегистровДляКонтроля(ЭтотОбъект.Движения, ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Запись наборов
	ПроведениеСерверПТБ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль результатов проведения
	ПроведениеСерверПТБ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	// Сообщим результат контроля пользователю
	МодульМенеджера.СообщитьКонтрольРезультатовПроведения(ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Очистка дополнительных свойств
	ПроведениеСерверПТБ.ОчиститьДополнительныеСвойства(ЭтотОбъект.ДополнительныеСвойства);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПроведениеСерверПТБ.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	МодульМенеджера = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ЭтотОбъект.Ссылка);

	// Предварительная подготовка параметров
	ПроведениеСерверПТБ.ИнициализацияДополнительныхСвойствДляПроведения(ЭтотОбъект.Ссылка, ЭтотОбъект.ДополнительныеСвойства, РежимПроведения);
	
	// Проверим наличие ручной корректировки по документу
	Если НЕ ПроведениеСерверПТБ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовка параметров для проведения
	МодульМенеджера.ПодготовитьПараметрыПроведения(ЭтотОбъект.Ссылка, ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Дополнительные проверки
	МодульМенеджера.ВыполнитьДополнительныеПроверки(ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Формирование движений до выполнения контроля
	МодульМенеджера.СформироватьДвиженияПоРегистрам(ЭтотОбъект.Движения, ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Добавляет регистры для выполнения контроля движений
	МодульМенеджера.СформироватьСписокРегистровДляКонтроля(ЭтотОбъект.Движения, ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Запись наборов
	ПроведениеСерверПТБ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль результатов проведения
	ПроведениеСерверПТБ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	// Сообщим результат контроля пользователю
	МодульМенеджера.СообщитьКонтрольРезультатовПроведения(ЭтотОбъект.ДополнительныеСвойства, Отказ);
	
	// Очистка дополнительных свойств
	ПроведениеСерверПТБ.ОчиститьДополнительныеСвойства(ЭтотОбъект.ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ОбработкаУдаленияПроведенияБезКонтроля(Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// Заполнение "по умолчанию"
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		ЭтотОбъект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Заполнение на основании или по данным переданной структуры
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоОбъектуОснованию(ДанныеЗаполнения);
	Иначе
		Если ТипДанныхЗаполнения = Тип("Структура") Тогда
			ОбщегоНазначенияПТБ.ЗаполнитьПоСтруктуре(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли 

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ВыделенныеСтроки", ВыделенныеСтроки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СписокПередУдалениемВопросЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='При удалении только записей об идентификаторах возможна "
		+ "рассинхронизация данных между 1С и Confluence. Вы уверены что хотите продолжить?'"), РежимДиалогаВопрос.ОКОтмена,
		60, КодВозвратаДиалога.Отмена,, КодВозвратаДиалога.Отмена);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьКонтент(Команда)
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьКонтентНаСервере(ВыделенныеСтроки);
	
	Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура СписокПередУдалениемВопросЗавершение(РезультатВопроса, ПараметрыВыполнения) Экспорт
	Если НЕ РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьЗаписиРегистраНаСервере(ПараметрыВыполнения.ВыделенныеСтроки);
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста 
Процедура УдалитьЗаписиРегистраНаСервере(знач МассивКлючи)
	Для Каждого КлючЗаписи Из МассивКлючи Цикл
		БазаЗнанийСсылка	= КлючЗаписи.БазаЗнаний;
		ЭлементСсылка		= КлючЗаписи.Элемент;
		
		УдалитьИдентификаторИзРегистраНаСервере(БазаЗнанийСсылка, ЭлементСсылка);
	КонецЦикла; 
КонецПроцедуры

&НаСервереБезКонтекста 
Процедура УдалитьКонтентНаСервере(знач МассивКлючи)
	Для Каждого КлючЗаписи Из МассивКлючи Цикл
		БазаЗнанийСсылка	= КлючЗаписи.БазаЗнаний;
		ЭлементСсылка		= КлючЗаписи.Элемент;
		
		НастройкиПодключения = ИнтеграцияConfluenceПереопределяемый.ПолучитьНастройкиПодключенияПоОбъекту(БазаЗнанийСсылка, ЭлементСсылка);
		Если НастройкиПодключения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИдентификаторВерсия = РегистрыСведений.ИдентификаторыБазыЗнанийConfluence.ПолучитьИдентификаторВерсию(БазаЗнанийСсылка, ЭлементСсылка);
		Если НЕ ПустаяСтрока(ИдентификаторВерсия.Идентификатор) Тогда
			Результат = ИнтеграцияConfluenceКлиентСервер.УдалитьКонтент(НастройкиПодключения, ИдентификаторВерсия.Идентификатор, Истина);
			Если ИнтеграцияConfluenceКлиентСервер.ПроверитьОшибки(Результат, Истина) Тогда
				Продолжить;
			КонецЕсли;
			
			ИнтеграцияConfluenceКлиентСервер.УдалитьКонтент(НастройкиПодключения, ИдентификаторВерсия.Идентификатор, Ложь);
		КонецЕсли;
		
		УдалитьИдентификаторИзРегистраНаСервере(БазаЗнанийСсылка, ЭлементСсылка);
	КонецЦикла; 
КонецПроцедуры

&НаСервереБезКонтекста 
Процедура УдалитьИдентификаторИзРегистраНаСервере(знач БазаЗнанийСсылка, знач ЭлементСсылка)
	НаборЗаписей = РегистрыСведений.ИдентификаторыБазыЗнанийConfluence.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.БазаЗнаний.Установить(БазаЗнанийСсылка);
	НаборЗаписей.Отбор.Элемент.Установить(ЭлементСсылка);
	НаборЗаписей.УдалитьКонтент = Истина;
	НаборЗаписей.Записать(Истина);
КонецПроцедуры

#КонецОбласти

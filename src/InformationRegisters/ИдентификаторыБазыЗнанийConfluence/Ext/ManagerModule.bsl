
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаписатьИдентификатор(знач БазаЗнанийСсылка, знач Объект, знач Тип, знач Идентификатор, знач Версия,
	знач АдресСсылки = "",
	знач Представление = "",
	знач ДатаОбновления = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьРегистра = РегистрыСведений.ИдентификаторыБазыЗнанийConfluence.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Элемент			= Объект;
	ЗаписьРегистра.БазаЗнаний		= БазаЗнанийСсылка;
	ЗаписьРегистра.ТипДанных		= Тип;
	ЗаписьРегистра.Идентификатор	= Идентификатор;
	ЗаписьРегистра.НомерВерсии		= Версия;
	ЗаписьРегистра.АдресСсылки		= АдресСсылки;
	ЗаписьРегистра.Представление	= Представление;
	ЗаписьРегистра.ДатаОбновления	= ?(НЕ ТипЗнч(ДатаОбновления) = Тип("Дата"), ТекущаяДатаСеанса(), ДатаОбновления);
	ЗаписьРегистра.Записать(Истина);
	
КонецПроцедуры

Функция ПолучитьИдентификаторВерсию(знач БазаЗнанийСсылка, знач Объект) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Элемент"		, Объект);
	Запрос.УстановитьПараметр("БазаЗнаний"	, БазаЗнанийСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИдентификаторыБазыЗнанийConfluence.Идентификатор КАК Идентификатор,
	|	ИдентификаторыБазыЗнанийConfluence.НомерВерсии КАК НомерВерсии
	|ИЗ
	|	РегистрСведений.ИдентификаторыБазыЗнанийConfluence КАК ИдентификаторыБазыЗнанийConfluence
	|ГДЕ
	|	ИдентификаторыБазыЗнанийConfluence.Элемент = &Элемент
	|	И ИдентификаторыБазыЗнанийConfluence.БазаЗнаний = &БазаЗнаний";
	
	ДанныеЗаполнения = Новый Структура("Идентификатор, НомерВерсии", "", 0);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Выборка);
	КонецЕсли;	

	Возврат ДанныеЗаполнения;	
КонецФункции

Функция ПолучитьСсылкиНаОбъекты(знач БазаЗнанийСсылка, знач МассивОбъектов) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БазаЗнаний"		, БазаЗнанийСсылка);
	Запрос.УстановитьПараметр("МассивОбъектов"	, МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИдентификаторыБазыЗнанийConfluence.Элемент КАК Элемент,
	|	ИдентификаторыБазыЗнанийConfluence.АдресСсылки КАК АдресСсылки
	|ИЗ
	|	РегистрСведений.ИдентификаторыБазыЗнанийConfluence КАК ИдентификаторыБазыЗнанийConfluence
	|ГДЕ
	|	ИдентификаторыБазыЗнанийConfluence.БазаЗнаний = &БазаЗнаний
	|	И ИдентификаторыБазыЗнанийConfluence.Элемент В(&МассивОбъектов)";
	
	Соответствие = Новый Соответствие; 
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.Элемент, Выборка.АдресСсылки);
	КонецЦикла;  
	
	Возврат Соответствие;
КонецФункции

Функция ПолучитьСсылкуНаОбъект(знач БазаЗнанийСсылка, знач Объект) Экспорт
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект);
	
	Результат = ПолучитьСсылкиНаОбъекты(БазаЗнанийСсылка, МассивОбъектов);
	
	АдресСсылки = Результат.Получить(Объект);
	Возврат ?(АдресСсылки = Неопределено, "", АдресСсылки);
КонецФункции

#КонецОбласти

#КонецЕсли 
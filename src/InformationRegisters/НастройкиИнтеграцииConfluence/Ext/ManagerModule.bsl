#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает все данные регистра по указанному пользователю и базе знаний.
// Если пользователь не указан, используется текущий пользователь
// Если база знаний не указана, используется основная база знаний
//
// Параметры:
//	ПользовательСсылка - СправочникСсылка.Пользователи
//	БазаЗнанийСсылка - СправочникСсылка.БазыЗнанийConfluence
//
// Возвращаемое значение:
//   Структура - см. РегистрСведений.НастройкиИнтеграцииConfluence
// 
Функция НастройкиПользователя(знач ПользовательСсылка = Неопределено, знач БазаЗнанийСсылка = Неопределено) Экспорт
	Если НЕ ЗначениеЗаполнено(ПользовательСсылка) Тогда
		ПользовательСсылка = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(БазаЗнанийСсылка) Тогда
		БазаЗнанийСсылка = Справочники.БазыЗнанийConfluence.ОсновнаяБазаЗнаний();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", ПользовательСсылка);
	Запрос.УстановитьПараметр("БазаЗнаний"	, БазаЗнанийСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииConfluence.*
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииConfluence КАК НастройкиИнтеграцииConfluence
	|ГДЕ
	|	НастройкиИнтеграцииConfluence.Пользователь = &Пользователь
	|	И НастройкиИнтеграцииConfluence.БазаЗнаний = &БазаЗнаний";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Настройки = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		МассивТипов = Колонка.ТипЗначения.Типы();
		ИндексNULL	= МассивТипов.Найти(Тип("Null"));
		Если ИндексNULL <> Неопределено Тогда
			МассивТипов.Удалить(ИндексNULL);
		КонецЕсли;
		
		ТипКолонки = Новый ОписаниеТипов(МассивТипов);
		
		Настройки.Вставить(Колонка.Имя, ТипКолонки.ПривестиЗначение(Неопределено));
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Настройки, Выборка);
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли 
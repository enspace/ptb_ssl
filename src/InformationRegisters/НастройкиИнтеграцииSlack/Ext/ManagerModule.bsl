#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьЗначениеНастройки(знач ПользовательСсылка, знач НастройкаСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Настройка"		, НастройкаСсылка);
	Запрос.УстановитьПараметр("Пользователь"	, ПользовательСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииSlack.Настройка КАК Настройка,
	|	НастройкиИнтеграцииSlack.ЗначениеСтрока20 КАК ЗначениеСтрока20,
	|	НастройкиИнтеграцииSlack.ЗначениеСтрока КАК ЗначениеСтрока,
	|	НастройкиИнтеграцииSlack.ЗначениеЧисло КАК ЗначениеЧисло,
	|	НастройкиИнтеграцииSlack.ЗначениеДата КАК ЗначениеДата,
	|	НастройкиИнтеграцииSlack.ЗначениеСсылка КАК ЗначениеСсылка,
	|	НастройкиИнтеграцииSlack.ЗначениеБулево КАК ЗначениеБулево,
	|	НастройкиИнтеграцииSlack.ИмяРесурса КАК ИмяРесурса,
	|	НастройкиИнтеграцииSlack.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииSlack КАК НастройкиИнтеграцииSlack
	|ГДЕ
	|	НастройкиИнтеграцииSlack.Настройка = &Настройка
	|	И НастройкиИнтеграцииSlack.Пользователь = &Пользователь";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка[Выборка.ИмяРесурса];
КонецФункции

Функция ПолучитьЗначенияНастроек(знач ПользовательСсылка, знач НастройкиКакСсылки = Ложь, знач ТолькоЗначения = Истина) Экспорт
	Если НастройкиКакСсылки Тогда
		ЗначенияНастроек = Новый Соответствие; 
	Иначе 
		ЗначенияНастроек = Новый Структура;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", ПользовательСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииSlack.Настройка КАК Настройка,
	|	НастройкиИнтеграцииSlack.ЗначениеСтрока20 КАК ЗначениеСтрока20,
	|	НастройкиИнтеграцииSlack.ЗначениеСтрока КАК ЗначениеСтрока,
	|	НастройкиИнтеграцииSlack.ЗначениеЧисло КАК ЗначениеЧисло,
	|	НастройкиИнтеграцииSlack.ЗначениеДата КАК ЗначениеДата,
	|	НастройкиИнтеграцииSlack.ЗначениеСсылка КАК ЗначениеСсылка,
	|	НастройкиИнтеграцииSlack.ЗначениеБулево КАК ЗначениеБулево,
	|	НастройкиИнтеграцииSlack.ИмяРесурса КАК ИмяРесурса,
	|	НастройкиИнтеграцииSlack.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииSlack КАК НастройкиИнтеграцииSlack
	|ГДЕ
	|	НастройкиИнтеграцииSlack.Пользователь = &Пользователь";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ЗначенияНастроек;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Выборка.Настройка) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НастройкиКакСсылки Тогда
			КлючЗначения = Выборка.Настройка;
		Иначе 
			КлючЗначения = ОбщегоНазначения.ИмяЗначенияПеречисления(Выборка.Настройка);
		КонецЕсли;
		
		ЗначениеНастройки = Выборка[Выборка.ИмяРесурса];
		
		Если ТолькоЗначения Тогда
			ЗначенияНастроек.Вставить(КлючЗначения, ЗначениеНастройки);
		Иначе 
			СтруктураЗначения = Новый Структура("Значение, Представление",
				ЗначениеНастройки,
				Выборка.Представление);
			ЗначенияНастроек.Вставить(КлючЗначения, СтруктураЗначения);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначенияНастроек;
КонецФункции

Функция УстановитьЗначениеНастройки(знач ПользовательСсылка, знач НастройкаСсылка, знач Значение) Экспорт
	ПараметрыНастройки = Перечисления.ВидыНастроекИнтеграцииSlack.ПолучитьДанныеНастройки(НастройкаСсылка);
	
	ЗаписьРегистра = РегистрыСведений.НастройкиИнтеграцииSlack.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Пользователь = ПользовательСсылка;
	ЗаписьРегистра.Настройка	= НастройкаСсылка;
	ЗаписьРегистра[ПараметрыНастройки.ИмяРесурса] = Значение;
	ЗаписьРегистра.ИмяРесурса	= ПараметрыНастройки.ИмяРесурса;
	ЗаписьРегистра.Записать(Истина);
КонецФункции

Функция ПолучитьПользователяПоКонтрольномуЧислу(знач КонтрольноеЧисло) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Настройка"		, Перечисления.ВидыНастроекИнтеграцииSlack.КонтрольноеЧисло);
	Запрос.УстановитьПараметр("ЗначениеЧисло"	, ?(ТипЗнч(КонтрольноеЧисло) = Тип("Число"), КонтрольноеЧисло, Число(КонтрольноеЧисло)));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииSlack.Пользователь КАК Пользователь
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииSlack КАК НастройкиИнтеграцииSlack
	|ГДЕ
	|	НастройкиИнтеграцииSlack.Настройка = &Настройка
	|	И НастройкиИнтеграцииSlack.ЗначениеЧисло = &ЗначениеЧисло";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();  
	
	Возврат Выборка.Пользователь;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли 
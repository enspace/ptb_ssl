// 21.10.2015, Белопухов А.Н., п.3388, НАЧАЛО
&НаСервере
Функция ВОбновленииЕстьПунктыПользователя(Пользователь, ВерсияКонфигунации) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СообщенияВТехническуюПоддержку.Идентификатор
	|ИЗ
	|	РегистрСведений.СообщенияВТехническуюПоддержку КАК СообщенияВТехническуюПоддержку
	|ГДЕ
	|	СообщенияВТехническуюПоддержку.НомерРелиза = &НомерРелиза
	|	И СообщенияВТехническуюПоддержку.Отправитель = &Отправитель";
	Запрос.УстановитьПараметр("Отправитель",Пользователь);
	Запрос.УстановитьПараметр("НомерРелиза",ВерсияКонфигунации);

	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

&НаСервере
Функция ПолучитьСписокРелизов(МассивСообщений = Неопределено) Экспорт
	//Заполним список релизов
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СообщенияВТехническуюПоддержку.НомерРелиза,
	|	МАКСИМУМ(СообщенияВТехническуюПоддержку.ДатаВыполнения) КАК ДатаВыполнения
	|ИЗ
	|	РегистрСведений.СообщенияВТехническуюПоддержку КАК СообщенияВТехническуюПоддержку
	|ГДЕ
	|	НЕ СообщенияВТехническуюПоддержку.НомерРелиза = """"
	|&ОтборСообщений
	|
	|СГРУППИРОВАТЬ ПО
	|	СообщенияВТехническуюПоддержку.НомерРелиза
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВыполнения УБЫВ";
	
	Запрос.УстановитьПараметр("МассивСообщений",	МассивСообщений);

	Если МассивСообщений = Неопределено ИЛИ МассивСообщений.Количество() = 0 Тогда
		
		ОтборСообщений = 
		"";
	Иначе
		ОтборСообщений = 

		"	И СообщенияВТехническуюПоддержку.Идентификатор В(&МассивСообщений)";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса,"&ОтборСообщений",ОтборСообщений);

	ТаблицаРелизов = Запрос.Выполнить().Выгрузить(); 
	
	Возврат ТаблицаРелизов;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокПунктовОбъектаНаСервере(ПолноеИмяОбъекта = "") Экспорт

	Возврат РегистрыСведений.СообщенияВТехническуюПоддержку.ПолучитьСписокПунктовОбъектаНаСервере(ПолноеИмяОбъекта);
	
КонецФункции

&НаСервере
Функция ПолучитьДатуРелиза(Релиз) Экспорт
	ТаблицаРелизов = ПолучитьСписокРелизов();
	НайденнаяСтрока = ТаблицаРелизов.Найти(Релиз);
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат Дата(1,1,1);
	Иначе
		Возврат НайденнаяСтрока.ДатаВыполнения;
	КонецЕсли;
КонецФункции

&НаСервере
Функция ПолучитьДанныеДляПечати(Пользователь, ВерсияКонфигурации, МассивСообщений = Неопределено) Экспорт
	Запрос = Новый Запрос;
	ТекстЗапроса =
	 "ВЫБРАТЬ
	 |	СообщенияВТехническуюПоддержку.Отправитель,
	 |	СообщенияВТехническуюПоддержку.ОписаниеИзменений,
	 |	СообщенияВТехническуюПоддержку.КодРазработчика,
	 |	СообщенияВТехническуюПоддержку.НомерРелиза,
	 |	СообщенияВТехническуюПоддержку.ДатаОтправки,
	 |	СообщенияВТехническуюПоддержку.ДатаВыполнения,
	 |	СообщенияВТехническуюПоддержку.Текст,
	 |	СообщенияВТехническуюПоддержку.ЗаданиеНаРазработку,
	 |	СообщенияВТехническуюПоддержку.Тема,
	 |	ВЫБОР
	 |		КОГДА СообщенияВТехническуюПоддержку.Отправитель = &Пользователь
	 |			ТОГДА ИСТИНА
	 |		ИНАЧЕ ЛОЖЬ
	 |	КОНЕЦ КАК СобственныйПункт
	 |ИЗ
	 |	РегистрСведений.СообщенияВТехническуюПоддержку КАК СообщенияВТехническуюПоддержку
	 |ГДЕ
	 |	СообщенияВТехническуюПоддержку.НомерРелиза = &НомерРелиза
	 | &УсловияЗапроса
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	СобственныйПункт УБЫВ,
	 |	СообщенияВТехническуюПоддержку.ДатаОтправки УБЫВ";
	Запрос.УстановитьПараметр("Пользователь",		Пользователь);
	Запрос.УстановитьПараметр("НомерРелиза",		ВерсияКонфигурации);
	Запрос.УстановитьПараметр("МассивСообщений", 	МассивСообщений);

	Если МассивСообщений = Неопределено ИЛИ МассивСообщений.Количество() = 0 Тогда
		УсловияЗапроса = 
		"";
	Иначе
		УсловияЗапроса = 

		"И СообщенияВТехническуюПоддержку.Идентификатор В(&МассивСообщений)";
	КонецЕсли;

	Запрос.Текст = СтрЗаменить(ТекстЗапроса,"&УсловияЗапроса",УсловияЗапроса);

 	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗапроса = РезультатЗапроса.Выгрузить();
	
	Возврат ТаблицаЗапроса;	
	
КонецФункции

&НаСервере
Функция ОписанияИзмененийЗагружены() Экспорт
	
	ПользовательОпределен = ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПользователь);

	//Проверяем, загружены ли описания к новому релизу
	РегистрСообщенияВТехПоддержку = Метаданные.РегистрыСведений.СообщенияВТехническуюПоддержку;
	ЕстьПравоЧтенияРегистраСообщений = ПравоДоступа("Чтение",РегистрСообщенияВТехПоддержку);
	
	Если ПользовательОпределен И ЕстьПравоЧтенияРегистраСообщений Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НомерРелиза",Метаданные.Версия);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СообщенияВТехническуюПоддержку.Идентификатор
		|ИЗ
		|	РегистрСведений.СообщенияВТехническуюПоддержку КАК СообщенияВТехническуюПоддержку
		|ГДЕ
		|	СообщенияВТехническуюПоддержку.НомерРелиза = &НомерРелиза";
		ОписанияЗагружены = НЕ Запрос.Выполнить().Пустой();
		
		Возврат ОписанияЗагружены;
	Иначе 
		Возврат Ложь;
	КонецЕсли;	
КонецФункции

&НаСервере
Функция ВыводитьОписанияИзменений() Экспорт
	ПользовательОпределен = ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПользователь);

	//НастройкаНеПоказыватьОписания = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "НеВыводитьИнформациюПриСменеРелиза");
	НастройкаНеПоказыватьОписания = Ложь;
	Если НастройкаНеПоказыватьОписания Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ОписанияИзмененийЗагружены() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//Проверяем, прочитал ли пользователь описания
	РегистрСостоянияПросмотра = Метаданные.РегистрыСведений.СостоянияПросмотраОписаний;
	ЕстьПравоРедактированияРегистраСостояний = ПравоДоступа("Редактирование",РегистрСостоянияПросмотра); 
		
	Если ПользовательОпределен И ЕстьПравоРедактированияРегистраСостояний Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Пользователь",ПараметрыСеанса.ТекущийПользователь);
		Запрос.УстановитьПараметр("ВерсияКонфигурации",Метаданные.Версия);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияПросмотраОписаний.Просмотрено
		|ИЗ
		|	РегистрСведений.СостоянияПросмотраОписаний КАК СостоянияПросмотраОписаний
		|ГДЕ
		|	СостоянияПросмотраОписаний.ВерсияКонфигурации = &ВерсияКонфигурации
		|	И СостоянияПросмотраОписаний.Пользователь = &Пользователь
		|	И СостоянияПросмотраОписаний.Просмотрено";
		
		ИзмененияНеПрочитаны = Запрос.Выполнить().Пустой();
        Возврат ИзмененияНеПрочитаны;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
// 21.10.2015, Белопухов А.Н., п.3388, КОНЕЦ

// 21.10.2015, Белопухов А.Н., п.3388
 
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВыводитьОписание = Регистрысведений.СостоянияПросмотраОписаний.ВыводитьОписанияИзменений();
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		Если НЕ ВыводитьОписание Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
        ЭтаФорма.Элементы.Просмотрено.Доступность 			= ВыводитьОписание;
		ЭтаФорма.Элементы.ФормаЗаписатьИЗакрыть.Доступность = Запись.Просмотрено;
		
		Запись.Пользователь = Пользователи.АвторизованныйПользователь();
		Запись.ВерсияКонфигурации = Метаданные.Версия;
	Иначе
		ЭтаФорма.Элементы.Просмотрено.Доступность 			= Ложь;
		ЭтаФорма.Элементы.ФормаЗаписатьИЗакрыть.Доступность = Ложь;
    КонецЕсли;
	
	ВОбновленииЕстьПунктыПользователя = Регистрысведений.СостоянияПросмотраОписаний.ВОбновленииЕстьПунктыПользователя(Запись.Пользователь, Запись.ВерсияКонфигурации);

	Печать();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ МЕТОДЫ

&НаСервере
Процедура Печать() 
	
	ТабличныйДокумент.Очистить();
		
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = РегистрыСведений.СостоянияПросмотраОписаний.ПолучитьМакет("Макет");
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	
	Область.Параметры.НомерРелиза = Запись.ВерсияКонфигурации;
	Область.Параметры.ДатаРелиза = Формат(РегистрыСведений.СостоянияПросмотраОписаний.ПолучитьДатуРелиза(Запись.ВерсияКонфигурации),"ДФ=dd.MM.yyyy");
	
	ТабДок.Вывести(Область);
		
	ПоказатьПовторяющиесяСтроки(ТабДок,Макет,Истина);
	ПоказатьПовторяющиесяСтроки(ТабДок,Макет,Ложь);
	
	//ТабДок.ОтображатьЗаголовки = Истина;
	//ТабДок.ОтображатьСетку = Истина;
	
	ТабличныйДокумент.Вывести(ТабДок);	
    
КонецПроцедуры

&НаСервере
Процедура ПоказатьПовторяющиесяСтроки(ТабДок,Макет,Собственные)
	
	ДанныеДляПечати = РегистрыСведений.СостоянияПросмотраОписаний.ПолучитьДанныеДляПечати(Запись.Пользователь, Запись.ВерсияКонфигурации);

	ОбластьТема 	= Макет.ПолучитьОбласть("Тема");
	ОбластьДетали 	= Макет.ПолучитьОбласть("Детали");
	
	ОбластьОписаниеЗаголовок 	= Макет.ПолучитьОбласть("ОписаниеЗаголовок");
    ОбластьОписание 			= Макет.ПолучитьОбласть("ОписаниеИзменений");

	Если Собственные И НЕ ВОбновленииЕстьПунктыПользователя Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Собственные И ВОбновленииЕстьПунктыПользователя Тогда
		ОбластьДругиеПункты = Макет.ПолучитьОбласть("ДругиеПункты");
		ТабДок.Вывести(ОбластьДругиеПункты);
       	ТабДок.НачатьГруппуСтрок("Группа Прочие", НЕ ВОбновленииЕстьПунктыПользователя);

	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ДанныеДляПечати Цикл
		Если СтрокаТаблицы.СобственныйПункт = Собственные Тогда
			ОбластьТема.Параметры.КодРазработчика 	= Формат(СтрокаТаблицы.КодРазработчика,"ЧГ=");
			ОбластьТема.Параметры.Тема 			= СтрокаТаблицы.Тема;
			ТабДок.Вывести(ОбластьТема);
			
			ОбластьДетали.Параметры.Текст			= СтрокаТаблицы.Текст;
			ОбластьДетали.Параметры.ДатаОтправки 	= Формат(СтрокаТаблицы.ДатаОтправки,"ДФ=dd.MM.yyyy");
			ОбластьДетали.Параметры.Отправитель 	= СтрокаТаблицы.Отправитель;		
			
			ТабДок.Вывести(ОбластьДетали);
			
			ТабДок.Вывести(ОбластьОписаниеЗаголовок);

			Если СтрокаТаблицы.ЗаданиеНаРазработку <> "" Тогда
				ОбластьЗадание = Макет.ПолучитьОбласть("Задание");	
				ОбластьЗадание.Параметры.ЗаданиеНаРазработку = СтрокаТаблицы.ЗаданиеНаРазработку;
				ТабДок.Вывести(ОбластьЗадание);
			КонецЕсли;
			Если СтрокаТаблицы.ОписаниеИзменений = "" Тогда
				ОбластьОписание.Параметры.ОписаниеИзменений = "Исправлено. Без описаний";
				ТабДок.Вывести(ОбластьОписание);
				
			Иначе
				ОбластьОписание.Параметры.ОписаниеИзменений = СтрокаТаблицы.ОписаниеИзменений;
				ТабДок.Вывести(ОбластьОписание);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	Если НЕ Собственные И ВОбновленииЕстьПунктыПользователя Тогда
   		ТабДок.ЗакончитьГруппуСтрок();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ПросмотреноПриИзменении(Элемент)
	ЭтаФорма.Элементы.ФормаЗаписатьИЗакрыть.Доступность = Запись.Просмотрено;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Запись.Просмотрено Тогда
		Запись.ДатаПрочтения = ТекущаяДата();
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры


// Copyright (c) 2023, ООО ПрогТехБизнес
// Лицензия Attribution 4.0 International (CC BY 4.0)

#Область ПрограммныйИнтерфейс

// Выполняет обновление строки представления настройки отбора
//
// Параметры:
//	Форма 					- УправляемаяФорма
//	ИмяРеквизитаКомпоновщик	- Строка - имя реквизита с типом КомпоновщикНастроекКомпоновкиДанных
//	ПустоеЗначение			- Строка - представление строки если настройка отбора не задана.
//		По умолчанию "Нажмите для настройки отбора"
// 
Процедура ОбновитьПредставлениеОтбора(Форма, знач ИмяРеквизитаКомпоновщик, знач ПустоеЗначение = "") Экспорт 
	Если ПустаяСтрока(ПустоеЗначение) Тогда
		ПустоеЗначение = НСтр("ru='Нажмите для настройки отбора...'");
	КонецЕсли;
	
	ИмяРеквизитаПредставление = ИмяРеквизитаПредставление(ИмяРеквизитаКомпоновщик);
	
	ПредставлениеОтбора = Строка(Форма[ИмяРеквизитаКомпоновщик].Настройки.Отбор);
	Форма[ИмяРеквизитаПредставление] = ?(ПустаяСтрока(ПредставлениеОтбора),
		ПустоеЗначение,
		ПредставлениеОтбора);
КонецПроцедуры

Функция ИмяРеквизитаАдресХранилища(знач ИмяРеквизитаКомпоновщик) Экспорт 
	Возврат ИмяРеквизитаКомпоновщик + "АдресХранилищаСКД";
КонецФункции

Функция ИмяРеквизитаПредставление(знач ИмяРеквизитаКомпоновщик) Экспорт 
	Возврат ИмяРеквизитаКомпоновщик + "НастройкиОтборПредставление";
КонецФункции

// Возвращает КлючОбъекта настройки для указанной формы. По умолчанию равен имени формы. В процедуре описываются исключения
// КлючОбъекта используется для хранения настроек в ХранилищеОбщихНастроек
//
// Параметры:
//	Форма - УправляемаяФорма
//
Функция КлючОбъектаНастроекПоФормеВладельца(знач Форма) Экспорт        
	Если ТипЗнч(Форма) <> Тип("ФормаКлиентскогоПриложения") Тогда  
		Возврат Неопределено;
	КонецЕсли;
	
	Если НРег(Форма.ИмяФормы) = Нрег("Обработка.СозданиеНаОснованииВФоне.Форма.Форма") Тогда
		ТипДокумента = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "Объект.ТипДокумента");
		Если ПустаяСтрока(ТипДокумента) Тогда
			Возврат Форма.ИмяФормы; 	
		Иначе
			Возврат СтрШаблон("%1/%2", Форма.ИмяФормы, ТипДокумента);
		КонецЕсли;
	Иначе
		Возврат Форма.ИмяФормы; 	
	КонецЕсли;
КонецФункции

#КонецОбласти

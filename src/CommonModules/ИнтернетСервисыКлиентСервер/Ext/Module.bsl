
#Область ПрограммныйИнтерфейс

// Разбирает строку с параметрами вида Имя1=Значение1<&Имя2=Значение2&...>
// в структуру с именами и значениями параметров
//
// Параметры
//	СтрокаКоманды - Тип: Строка.
//
// Возвращаемое значение
//	Структура
//		Ключ - Строка. Имя параметра
//		Значение - Строка. Значение параметра
//
Функция ПолучитьПараметрыКоманды(СтрокаКоманды) Экспорт
	СтруктураПараметров = Новый Структура;
	
	ДанныеПараметров = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаКоманды, "&");
	Для Каждого Параметр Из ДанныеПараметров Цикл
		ДанныеПараметра = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Параметр, "=");
		Если ДанныеПараметра.Количество() > 1 Тогда
			СтруктураПараметров.Вставить(ДанныеПараметра[0], ДанныеПараметра[1]);
		ИначеЕсли ДанныеПараметра.Количество() = 1 Тогда
			СтруктураПараметров.Вставить(ДанныеПараметра[0], "");
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураПараметров;
КонецФункции

// Выводит сообщение об ошибке. При необходимости вызывает исключение
//
// Параметры:
//	Текст				- Строка - текст сообщения
//	ВызыватьИсключение	- Булево - признак использования исключения
//	НеСообщать			- Булево - не выводит сообщение
// 
Процедура ОбработатьТекстОшибки(знач Текст, знач ВызыватьИсключение = Ложь, знач НеСообщать = Ложь) Экспорт
	Если НЕ НеСообщать = Истина Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
	Если ВызыватьИсключение Тогда
		ВызватьИсключение Текст;
	КонецЕсли;
КонецПроцедуры

// Выводит сообщение об ошибке HTTP запроса
//
// Параметры:
//	ОтветHTTP			- ОтветHTTP
//	ВызыватьИсключение	- Булево - признак использования исключения
//	НеСообщать			- Булево - не выводит сообщение
// 
Процедура ОбработатьОшибкуЗапроса(знач ОтветHTTP, знач ВызыватьИсключение = Ложь, знач НеСообщать = Ложь) Экспорт
	Перем ТекстОшибки;
	
	СтрТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
	Если НЕ ПустаяСтрока(СтрТелоОтвета) Тогда
		Если СтрНачинаетсяС(СтрТелоОтвета, "{") Тогда
			СтруктураОшибки = ЗначениеИзСтрокиJSON(СтрТелоОтвета, Истина);
			ТекстОшибки = СтруктураОшибки.Получить("text");
		КонецЕсли;
	КонецЕсли;
	
	ТекстСообщения = СтрШаблон(НСтр("ru='Произошла ошибка при подключении к сервису (код: %1%2).'"),
		Строка(ОтветHTTP.КодСостояния),
		?(ЗначениеЗаполнено(ТекстОшибки), "; " + ТекстОшибки, ""));
		
	ОбработатьТекстОшибки(ТекстСообщения, ВызыватьИсключение, НеСообщать);
КонецПроцедуры

// Формирует объект HTTPЗапрос согласно настройкам
//
// Параметры:
//	АдресРесурса	- Строка - адрес ресурса запроса без учета адреса сервера (/ не обязателен в конце)
//		Например: /hs/partners/
//	Параметры			- Структура - параметры для формирования строки параметров ?key=value[&key1=value1]
//		Ключ 		- Строка - имя параметра
//		Значение	- Строка - значение параметра
//	Заголовки		- Соответствие - значения заголовков для подстановки в запрос
//
// Возвращаемое значение:
//	HTTPЗапрос
//
Функция ПолучитьHTTPЗапрос(знач АдресРесурса, знач Параметры = Неопределено, знач Заголовки = Неопределено) Экспорт
	ПараметрыКоллекция = (ТипЗнч(Параметры) = Тип("Соответствие")
		ИЛИ ТипЗнч(Параметры) = Тип("Структура"));
	
	Если НЕ ПараметрыКоллекция = Истина Тогда
		Параметры = Новый Соответствие;
	КонецЕсли;
	Если НЕ ТипЗнч(Заголовки) = Тип("Соответствие") Тогда
		Заголовки = Новый Соответствие;
	КонецЕсли;
	
	СтрПараметры = "";
	Для Каждого КлючИЗначение Из Параметры Цикл 
		Если НЕ ЗначениеЗаполнено(КлючИЗначение.Ключ) ИЛИ НЕ ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрПараметры = СтрПараметры
			+ ?(ПустаяСтрока(СтрПараметры), "", "&")
			+ СтрШаблон("%1=%2", КлючИЗначение.Ключ, КлючИЗначение.Значение);		
	КонецЦикла;
	
	Если НЕ СтрНачинаетсяС(АдресРесурса, "/") Тогда
		АдресРесурса = "/" + АдресРесурса;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(СтрПараметры) Тогда
		ШаблонАдреса = "%1?%2";
	Иначе 
		ШаблонАдреса = "%1%2";
	КонецЕсли;
	
	ПолныйАдрес = СтрШаблон(ШаблонАдреса, АдресРесурса, СтрПараметры);
	Возврат Новый HTTPЗапрос(ПолныйАдрес, Заголовки);
КонецФункции

// Формирует объект HTTPСоединение согласно настройкам
//
// Параметры:
//	АдресРесурса		- Строка, Структура - ссылка на ресурс в формате URI или структура
//		Структура - см. ОбщегоНазначенияКлиентСервер.СтруктураURI
//	Пользователь		- Строка - значение имени пользователя для подключения
//	ПарольПользователя	- Строка - значение пароля для подключения
//	ИспользоватьПрокси	- Булево - при установке Истина используется бесплатный прокси
//
// Возвращаемое значение:
//	HTTPСоединение
//
Функция ПолучитьHTTPСоединение(знач АдресРесурса, знач Пользователь = "", знач ПарольПользователя = "", знач ИспользоватьПрокси = Ложь, знач Таймаут = Неопределено) Экспорт
	Если ТипЗнч(АдресРесурса) = Тип("Структура") Тогда
		СтруктураАдреса = АдресРесурса;
	Иначе 
		СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресРесурса);
	КонецЕсли;
	
	Если СтруктураАдреса.Схема = "https" Тогда
		БезопасноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	Иначе 
		БезопасноеСоединение = Неопределено;
	КонецЕсли;
	
	Если ИспользоватьПрокси = Истина Тогда
		ИнтернетПрокси = ПолучитьНастройкиПрокси();
	КонецЕсли;
	
	СоединениеHTTP = Новый HTTPСоединение(СтруктураАдреса.ИмяСервера,,
		Пользователь,
		ПарольПользователя,,
		Таймаут,
		БезопасноеСоединение);
		
	Возврат СоединениеHTTP;
КонецФункции

// Проверяет что тип объекта - HTTPСоединение и адрес сервера задан
//
// Параметры:
//	СоединениеHTTP 		- HTTPСоединение
//	ВызыватьИсключение	- Булево - признак использования исключения
//	НеСообщать			- Булево - не выводит сообщение
//
// Возвращаемое значение:
//   Булево
// 
Функция ПроверитьHTTPСоединение(знач СоединениеHTTP, знач ВызыватьИсключение = Ложь, знач НеСообщать = Ложь) Экспорт
	Результат = (ТипЗнч(СоединениеHTTP) = Тип("HTTPСоединение") И НЕ ПустаяСтрока(СоединениеHTTP.Сервер));
	
	Если НЕ Результат Тогда
		ТекстСообщения = НСтр("ru='Ошибка проверки интернет-соединения с ИБ.'");
		ИнтернетСервисыКлиентСервер.ОбработатьТекстОшибки(ТекстСообщения, ВызыватьИсключение, НеСообщать);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

// Возвращает объект ИнтернетПрокси с бесплатным прокси полученным с сайта https://gimmeproxy.com
//
// Возвращаемое значение:
//   ИнтернетПрокси, Неопределено
// 
Функция ПолучитьНастройкиПрокси() Экспорт
	Параметры = Новый Соответствие;
	Параметры.Вставить("protocol"		, "http");
	Параметры.Вставить("post"			, "false");
	Параметры.Вставить("maxCheckPeriod"	, "3600");
	Параметры.Вставить("anonymityLevel"	, "0");
	
	ЗапросHTTP = ПолучитьHTTPЗапрос("api/getProxy", Параметры);
	Соединение = ПолучитьHTTPСоединение("https://gimmeproxy.com"); 
	
	Результат = Соединение.Получить(ЗапросHTTP);
	Если Результат.КодСостояния <> 200 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ТелоРезультата = Результат.ПолучитьТелоКакСтроку();
	
	ДанныеПрокси = ЗначениеИзСтрокиJSON(ТелоРезультата, Истина);
	
	Протокол	= ДанныеПрокси.Получить("protocol");
	Сервер		= ДанныеПрокси.Получить("ip");
	Порт		= ДанныеПрокси.Получить("port");
	ПортЧисло	= ?(Порт = Неопределено, Неопределено,
		СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Порт));
	
	Если Протокол = Неопределено ИЛИ Сервер = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	ИнтернетПрокси = Новый ИнтернетПрокси;
	ИнтернетПрокси.Установить(Протокол, Сервер, ПортЧисло,,, Ложь);
		
	Возврат ИнтернетПрокси;
КонецФункции

// Возвращает структуру с описанием текста ошибки
//
// Параметры:
//	ТекстОшибки - Строка
//
// Возвращаемое значение:
//   Структура
//		type - Строка - "error"
//		text - Строка - значение параметра ТекстОшибки
// 
Функция СтруктураError(знач ТекстОшибки = "") Экспорт
	Возврат Новый Структура("type, text", "error", ТекстОшибки);
КонецФункции

#КонецОбласти

#Область JSON

// Возвращает настройку для управления правилами сериализации значений в JSON
//
//	ВАЖНО:	При использовании РасширеннаяСериализация = Истина дополнительно вызывается обработчик
// 			Сервер: ИнтернетСервисыПереопределяемый.СериализацияЗначенияВJSON
// 			Клиент: ИнтернетСервисыКлиентПереопределяемый.СериализацияЗначенияВJSON
//
// Параметры:
//	РасширеннаяСериализация 	- Булево - признак использования расширенной сериализации значений
//		Для сервера см. ИнтернетСервисы.СериализацияЗначенияВJSON, для клиента доп. обработка не выполняется
//	ИспользоватьИсключение		- Булево - вызывать исключение, если есть свойство с неподдерживаемым типом
//		* Используется если РасширеннаяСериализация = Ложь
//	ОтказПриОшибкеСериализации	- Булево - если значение не поддерживает сериализацию в JSON
//		будет произведет отказ от записи свойства
//		* Используется если РасширеннаяСериализация = Ложь И ИспользоватьИсключение = Ложь
//	ТекстПриОшибкиСериалиазации	- Строка - текст значения для записи при ошибке сериализации
//		* Используется если РасширеннаяСериализация = Ложь И ОтказПриОшибкеСериализации = Ложь
//
// Возвращаемое значение:
//   ТипВид - описание возвращаемого значения
//
Функция ПараметрыЗначениеВСтрокуJSON() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("РасширеннаяСериализация"		, Истина);
	Параметры.Вставить("ИспользоватьИсключение"			, Истина);
	Параметры.Вставить("ОтказПриОшибкеСериализации"		, Ложь);
	Параметры.Вставить("ТекстПриОшибкиСериалиазации"	, НСтр("ru='(ошибка сериализации значения)'"));
	
	Возврат Параметры;
КонецФункции

// Преобразует значение в строку JSON, поддерживает сериализацию типов недоступных в методе ЗаписатьJSON
// Список поддерживаемых типов см. ИнтернетСервисыКлиентСервер.СериализацияЗначенияВJSON	                                    
//
// ВАЖНО: Для платформы 8.3.6 и ниже используется сериализация через общий модуль JSON, параметры игнорируются
//
// Параметры:
//	ОбъектДанных	- Любое значение
//	Настройки		- НастройкиСериализацииJSON
//	Параметры		- Структура, Неопределено - см. ИнтернетСервисыКлиентСервер.ПараметрыЗначениеВСтрокуJSON
//
// Возвращаемое значение:
//   Строка
// 
Функция ЗначениеВСтрокуJSON(знач ОбъектДанных, знач Настройки = Неопределено, знач Параметры = Неопределено) Экспорт
	Результат = "";
	
	Если НЕ ТипЗнч(Параметры) = Тип("Структура") Тогда
		Параметры = ПараметрыЗначениеВСтрокуJSON();
	КонецЕсли;
	
	Если ИнтернетСервисыКлиентСерверПовтИсп.ИспользоватьЗаписьJSON() Тогда
		ОбработкаПоУмолчанию = Истина;
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		
		Если Параметры.РасширеннаяСериализация Тогда 
			ИнтернетСервисыВызовСервера.ПодготовитьПараметрыСериализацииВJSON(Параметры);
			ЗаписатьJSON(ЗаписьJSON, ОбъектДанных, Настройки, "СериализацияЗначенияВJSON", ИнтернетСервисыКлиентСервер, Параметры);
		Иначе 
			ЗаписатьJSON(ЗаписьJSON, ОбъектДанных, Настройки, "ОбработкаОшибкиСериализацииВJSON", ИнтернетСервисыКлиентСервер, Параметры);
		КонецЕсли;
		
		Результат = ЗаписьJSON.Закрыть();
	Иначе 
		Результат = ИнтернетСервисыВызовСервера.ПолучитьСтрокуJSON(ОбъектДанных); 
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Преобразует строку JSON в значение
//
// Параметры:
//	СтрокаJSON		- Строка
//	КакСоответствие - Булево
//	СвойстваДаты	- Строка - имена свойств с датами
//	ФорматДаты		- ФорматДатыJSON
//
// Возвращаемое значение:
//   ЛюбоеЗначение
// 
Функция ЗначениеИзСтрокиJSON(знач СтрокаJSON,
	знач КакСоответствие = Ложь,
	знач СвойстваДаты = "",
	знач ФорматДаты = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ФорматДаты) = Тип("ФорматДатыJSON") Тогда
		ФорматДаты = ФорматДатыJSON.ISO;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Значение = ПрочитатьJSON(ЧтениеJSON, КакСоответствие, СвойстваДаты, ФорматДаты);
	
	ЧтениеJSON = Неопределено;
	
	Возврат Значение;
КонецФункции

// Выполняет преобразование значения в строку JSON для неподдерживаемых типов
// Используется для дополнения в методе ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON
// 
// По умолчанию сериализует значения следующих типов:
//	- Тип - строковое представление типа
//	- ОписаниеТипов - массив строковых представлений типа
//	- ПеречислениеСсылка - строка Перечисление.ИмяЗначенияПеречисления
//	- ЛюбаяСсылка - идентификатор ссылки или структура ПолноеИмя,Идентификатор,Представление
//	- УникальныйИдентификатор - строка
// 
// Если значение не входит в состав поддерживаемых типов, вызывается метод ИнтернетСервисыПереопределяемый.ПреобразоватьЗначениеВJSON
//
// Параметры:
//	Свойство	- Строка - имя свойства
//	Значение	- Произвольный - значение свойства
//	Параметры	- Структура - см. ИнтернетСервисыКлиентСервер.ПараметрыЗначениеВСтрокуJSON
//	Отказ		- Булево
//
// Возвращаемое значение:
//   Строка, Число, Булево - значение для подстановки в JSON
//
Функция СериализацияЗначенияВJSON(знач Свойство, знач Значение, знач Параметры, Отказ) Экспорт
	СтандартнаяОбработка = Истина;
	
	#Если Сервер Тогда				
		Результат = ИнтернетСервисыПереопределяемый.СериализацияЗначенияВJSON(Свойство, Значение, Параметры, СтандартнаяОбработка);
	#Иначе
		Результат = ИнтернетСервисыКлиентПереопределяемый.СериализацияЗначенияВJSON(Свойство, Значение, Параметры, СтандартнаяОбработка);
	#КонецЕсли

	Если НЕ СтандартнаяОбработка = Истина Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТипВсеСсылки	= Параметры.ТипВсеСсылки;
	ТипПеречисление	= Параметры.ТипПеречисление;
	
	ТипЗначения = ТипЗнч(Значение);
	Если ТипЗначения = Тип("УникальныйИдентификатор") Тогда
		Результат = Строка(Значение);
	ИначеЕсли ТипЗначения = Тип("Тип") Тогда
		Результат = ИнтернетСервисыВызовСервера.ПодготовитьЗначениеТип(Значение);
	ИначеЕсли ТипЗначения = Тип("ОписаниеТипов") Тогда
		Результат = ИнтернетСервисыВызовСервера.ПодготовитьЗначениеОписаниеТипов(Значение);
	ИначеЕсли ТипЗнч(ТипПеречисление) = Тип("ОписаниеТипов") И ТипПеречисление.СодержитТип(ТипЗначения) Тогда
		Результат = ИнтернетСервисыВызовСервера.ПодготовитьЗначениеПеречисление(Значение);
	ИначеЕсли ТипЗнч(ТипВсеСсылки) = Тип("ОписаниеТипов") И ТипВсеСсылки.СодержитТип(ТипЗначения) Тогда
		Результат = Строка(Значение.УникальныйИдентификатор());
	Иначе
		Результат = Строка(Значение);
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции

// Обработка сериализации неподдерживаемых типов, см. ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON
//
// Параметры:
//	Свойство	- Строка - имя свойства
//	Значение	- Произвольный - значение свойства
//	Параметры	- Структура - см. ИнтернетСервисыКлиентСервер.ПараметрыЗначениеВСтрокуJSON
//	Отказ		- Булево
//
// Возвращаемое значение:
//   зависит от параметров
//
Функция ОбработкаОшибкиСериализацииВJSON(знач Свойство, знач Значение, знач Параметры, Отказ) Экспорт
	Если Параметры.ИспользоватьИсключение Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru='Ошибка сериализации свойства ""%1"" (значение: %2)'"),
			Свойство,
			Строка(Значение));
			
		ВызватьИсключение ТекстОшибки;
	ИначеЕсли Параметры.ОтказПриОшибкеСериализации Тогда 
		Отказ = Истина;
	Иначе 
		Возврат Параметры.ТекстПриОшибкиСериалиазации;
	КонецЕсли;
КонецФункции

#КонецОбласти

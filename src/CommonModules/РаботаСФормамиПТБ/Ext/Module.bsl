
#Область ПрограммныйИнтерфейс

// Добавляет реквизит в форму на основании таблицы значений
//
// Параметры:
//	Форма - УправляемаяФорма
//	ИмяТабличнойЧасти - Строка
//	ТаблицаЗначений - ТаблицаЗначений
//	СохраняемыеДанные - Булево
// 
Процедура СоздатьРеквизитыФормыПоТаблицеЗначений(Форма, ИмяТабличнойЧасти, ТаблицаЗначений, СохраняемыеДанные = Ложь) Экспорт
	МассивДобавить = Новый Массив;
	
	МассивДобавить.Добавить(Новый РеквизитФормы(ИмяТабличнойЧасти, Новый ОписаниеТипов("ТаблицаЗначений"),,, СохраняемыеДанные));
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		НовыйРеквизитФормы = Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, ИмяТабличнойЧасти, Колонка.Заголовок);
		МассивДобавить.Добавить(НовыйРеквизитФормы);
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(МассивДобавить);
	
	Форма.ЗначениеВРеквизитФормы(ТаблицаЗначений, ИмяТабличнойЧасти);
КонецПроцедуры

#КонецОбласти

#Область МетодыРаботыСКопиейОбъекта

// Создает служебный реквизит для хранения копии данных реквизитов формы
// подробнее об использовании см. РаботаСФормамиКлиентСервер.СкопироватьДанныеФормы
//
// Параметры:
//	Форма - УправляемаяФорма - форма по данным которой надо получить значения реквизитов
// 
Процедура СоздатьРеквизитХраненияКопииДанныхФормы(Форма) Экспорт
	
	РеквизитКопия = "_Служебный_КопияДанных_";
	
	Попытка
		ЗначениеРеквизита = Форма._Служебный_КопияДанных_;
		РеквизитСуществует = Истина;
	Исключение
		РеквизитСуществует = Ложь;
	КонецПопытки;
	
	Если РеквизитСуществует Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитКопии = Новый РеквизитФормы(РеквизитКопия, Новый ОписаниеТипов("Неопределено"));
	
	МассивДобавить = Новый Массив;
	МассивДобавить.Добавить(РеквизитКопии);
	
	Форма.ИзменитьРеквизиты(МассивДобавить);
	
КонецПроцедуры

#КонецОбласти



#Область ПрограммныйИнтерфейс

// Процедура собирает сведения о ВСЕХ реквизитах формы с типом = "ТаблицаФормы" 
// 		и сохраняет в реквизит формы "_Служебный_ОписаниеТаблицДляГрупповогоРедактирования_"(см. ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаНастроекГрупповогоРедактирования()). 
//
// Источником таблицы формы могут быть реквизиты формы ("ТаблицаЗначений", "ДеревоЗначений), так и табличные части объекта.
// 
// Параметры:
//	Форма				- УправляемаяФорма - произвольная форма справочника, документа, обработки, содержащая таблицы формы.
// 	ИсключаемыеТаблицы 	- Строка - Имена элементов формы с типом "ТаблицаФормы" для которых запрещается использование групповой обработки. Например "ТаблицаНоменклатуры, ДеревоМатериалов". 
// 	ИсключаемыеКолонки 	- Строка - Имена элементов формы с типом "ПолеФормы" или "ГруппаФормы" (колонки или группы колонок таблицыФормы) для которых запрещается использование групповой обработки. Например "Цена, НомерСтроки".
//ВНИМАНИЕ! Колонки таблицы невидимые или недоступные пользователю указывать не нужно, они групповой обработкой не редактируются. 	
//	ОтображениеСуммыВыделенныхСтрок - Соответствие - Ключ - Имя ТаблицыФормы (для которой вычисляем суммы), Значение - Имя ГруппаФормы(в которой создаем реквизиты расчета)
//
Процедура СформироватьОписаниеТабличныхЧастейФормы(Форма, знач Настройки) Экспорт
	
	ДоступныеТаблицы = Настройки.ДоступныеТаблицы;
	ДоступныеКолонки = Настройки.ДоступныеКолонки;
	НедоступныеКолонки = Настройки.НедоступныеКолонки;
	
	Если ДоступныеТаблицы.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	СуществующиеРеквизиты = ПолучитьСоответствиеСуществующихЭлементовФормы(Форма);
	
	МассивДобавляемыхРеквизитов = Новый Массив;
	
	ИмяРеквизитаОписанияТаблицФормы = ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаНастроекГрупповогоРедактирования();
	
	Если СуществующиеРеквизиты.Получить(ИмяРеквизитаОписанияТаблицФормы) = Неопределено Тогда
 		ДобавитьРеквизитХраненияОписанияТаблицФормы(Форма, МассивДобавляемыхРеквизитов, ИмяРеквизитаОписанияТаблицФормы);
	КонецЕсли;                                                                         
	
	ЭлементыФормы = Форма.Элементы;
	
	ОписаниеТаблиц = Новый Соответствие;
		
	Для Каждого ИмяТаблицы Из ДоступныеТаблицы Цикл  
		
		ЭлементТаблица = ЭлементыФормы.Найти(ИмяТаблицы);
		Если ЭлементТаблица = Неопределено Тогда
			Продолжить;				
		КонецЕсли; 
		
		Если ТипЗнч(ЭлементТаблица) <> Тип("ТаблицаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		//Добавим данные по колонке в ОписаниеТаблиц
		ТаблицаИмяЭлемента = ЭлементТаблица.Имя;
		ТаблицаПутьКДанным = ЭлементТаблица.ПутьКДанным;
		
		// Получаем только те колонки таблиц, которые добавлены на форму в качестве элементов и не являются исключенными
		СоответствиеКолонокНаФорме 	= Новый Соответствие;
		ЕстьДоступныеКолонки 		= Ложь;
		
		КолонкиЭлементТаблица = ЭлементТаблица.ПодчиненныеЭлементы;
		Для Каждого ЭлементКолонка Из КолонкиЭлементТаблица Цикл  
				
			Если ЗначениеЗаполнено(ДоступныеКолонки) И ДоступныеКолонки.Найти(ЭлементКолонка.Имя) = Неопределено Тогда
				Продолжить;	
			КонецЕсли;                                                                           

			ПроверитьКолонкуТаблицы(Форма, ТаблицаПутьКДанным, ЭлементКолонка, СоответствиеКолонокНаФорме, ДоступныеКолонки, НедоступныеКолонки, ЕстьДоступныеКолонки, Ложь) 	
		КонецЦикла;

		ОписаниеТаблиц.Вставить(ТаблицаИмяЭлемента, ПолучитьСтруктуруОписанияТаблицы());				
		
		// Формируем описание колонок таблицы
		МассивКолонокТаблицы = Форма.ПолучитьРеквизиты(ТаблицаПутьКДанным);

		СоответствиеКолонокТаблицы = Новый Соответствие;
		Для каждого ЭлементМассива Из МассивКолонокТаблицы Цикл
		    НайденныйЭлемент = СоответствиеКолонокНаФорме.Получить(ЭлементМассива.Имя);
			
			Если НайденныйЭлемент = Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			
			СтруктураКолонки = ПолучитьСтруктуруОписанияКолонки();
			СтруктураКолонки.Имя			= ЭлементМассива.Имя;	
			СтруктураКолонки.Путь			= ЭлементМассива.Путь;
			СтруктураКолонки.Заголовок		= ЭлементМассива.Заголовок;
			СтруктураКолонки.ТипЗначения	= ЭлементМассива.ТипЗначения;
			СтруктураКолонки.ИмяЭлемента	= НайденныйЭлемент;
		
			СоответствиеКолонокТаблицы.Вставить(НайденныйЭлемент, СтруктураКолонки)
		КонецЦикла;
		
		СтруктураТаблицы = ПолучитьСтруктуруОписанияТаблицы();
		СтруктураТаблицы.ТаблицаИмяЭлемента	= ТаблицаИмяЭлемента;
		СтруктураТаблицы.ТаблицаПутьКДанным	= ТаблицаПутьКДанным;
		СтруктураТаблицы.ОписаниеКолонок	= СоответствиеКолонокТаблицы;
		
		ОписаниеТаблиц.Вставить(ТаблицаИмяЭлемента, СтруктураТаблицы);
 	   		
		//ЕстьДоступныеКолонки - Если у таблицы есть хотя бы одна доступная колонка, то добавляем команду
		Если ЕстьДоступныеКолонки Тогда
			ДобавитьКнопкуВКоманднуюПанельТаблицы(Форма, ЭлементТаблица, Настройки.НастройкиРазмещения);			
		КонецЕсли; 
	КонецЦикла;

	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);

	Форма[ИмяРеквизитаОписанияТаблицФормы] = Новый ФиксированноеСоответствие(ОписаниеТаблиц);
	
КонецПроцедуры

// Процедура собирает сведения о ВСЕХ реквизитах формы с типом = "ТаблицаФормы" 
// 		и сохраняет в реквизит формы "_Служебный_ОписаниеТаблицДляГрупповогоРедактирования_"(см. ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаНастроекГрупповогоРедактирования()). 
//
// Источником таблицы формы могут быть реквизиты формы ("ТаблицаЗначений", "ДеревоЗначений), так и табличные части объекта.
// 
// Параметры:
//	Форма				- УправляемаяФорма - произвольная форма справочника, документа, обработки, содержащая таблицы формы.
// 	ИсключаемыеТаблицы 	- Строка - Имена элементов формы с типом "ТаблицаФормы" для которых запрещается использование групповой обработки. Например "ТаблицаНоменклатуры, ДеревоМатериалов". 
// 	ИсключаемыеКолонки 	- Строка - Имена элементов формы с типом "ПолеФормы" или "ГруппаФормы" (колонки или группы колонок таблицыФормы) для которых запрещается использование групповой обработки. Например "Цена, НомерСтроки".
//ВНИМАНИЕ! Колонки таблицы невидимые или недоступные пользователю указывать не нужно, они групповой обработкой не редактируются. 	
//	ОтображениеСуммыВыделенныхСтрок - Соответствие - Ключ - Имя ТаблицыФормы (для которой вычисляем суммы), Значение - Имя ГруппаФормы(в которой создаем реквизиты расчета)
//
Процедура ОписаниеТабличныхЧастейДляСуммированияСтрок(Форма, знач Настройки, ДобавлятьКнопкиВКоманднуюПанельТаблицыФормы = Истина, ОтображениеСуммыВыделенныхСтрок = Неопределено)  Экспорт
	
	ДоступныеТаблицы = Настройки.ДоступныеТаблицы;
	ДоступныеКолонки = Настройки.ДоступныеКолонки;
	НедоступныеКолонки = Настройки.НедоступныеКолонки;
	
	Если ДоступныеТаблицы.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	СуществующиеРеквизиты = ПолучитьСоответствиеСуществующихЭлементовФормы(Форма);
	
	МассивДобавляемыхРеквизитов = Новый Массив;
	
	ИмяРеквизитаОписанияТаблицФормы = ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаНастроекСуммирования();
	
	Если СуществующиеРеквизиты.Получить(ИмяРеквизитаОписанияТаблицФормы) = Неопределено Тогда
 		ДобавитьРеквизитХраненияОписанияТаблицФормы(Форма, МассивДобавляемыхРеквизитов, ИмяРеквизитаОписанияТаблицФормы);
	КонецЕсли;                                                                         
	
	ЭлементыФормы = Форма.Элементы;
	
	ОписаниеТаблиц = Новый Соответствие;
		
	Для Каждого ИмяТаблицы Из ДоступныеТаблицы Цикл  
		
		ЭлементТаблица = ЭлементыФормы.Найти(ИмяТаблицы);
		Если ЭлементТаблица = Неопределено Тогда
			Продолжить;				
		КонецЕсли; 
		
		Если ТипЗнч(ЭлементТаблица) <> Тип("ТаблицаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		//Добавим данные по колонке в ОписаниеТаблиц
		ТаблицаИмяЭлемента = ЭлементТаблица.Имя;
		ТаблицаПутьКДанным = ЭлементТаблица.ПутьКДанным;
		
		// Получаем только те колонки таблиц, которые добавлены на форму в качестве элементов и не являются исключенными
		СоответствиеКолонокНаФорме 	= Новый Соответствие;
		ЕстьДоступныеКолонки 		= Ложь;
		
		КолонкиЭлементТаблица = ЭлементТаблица.ПодчиненныеЭлементы;
		Для Каждого ЭлементКолонка Из КолонкиЭлементТаблица Цикл  
				
			Если ЗначениеЗаполнено(ДоступныеКолонки) И ДоступныеКолонки.Найти(ЭлементКолонка.Имя) = Неопределено Тогда
				Продолжить;	
			КонецЕсли;                                                                           

			ПроверитьКолонкуТаблицы(Форма, ТаблицаПутьКДанным, ЭлементКолонка, СоответствиеКолонокНаФорме, ДоступныеКолонки, НедоступныеКолонки, ЕстьДоступныеКолонки, Истина) 	
		КонецЦикла;

		ОписаниеТаблиц.Вставить(ТаблицаИмяЭлемента, ПолучитьСтруктуруОписанияТаблицы());				
		
		// Формируем описание колонок таблицы
		МассивКолонокТаблицы = Форма.ПолучитьРеквизиты(ТаблицаПутьКДанным);

		СоответствиеКолонокТаблицы = Новый Соответствие;
		Для каждого ЭлементМассива Из МассивКолонокТаблицы Цикл
		    НайденныйЭлемент = СоответствиеКолонокНаФорме.Получить(ЭлементМассива.Имя);
			
			Если НайденныйЭлемент = Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			
			СтруктураКолонки = ПолучитьСтруктуруОписанияКолонки();
			СтруктураКолонки.Имя			= ЭлементМассива.Имя;	
			СтруктураКолонки.Путь			= ЭлементМассива.Путь;
			СтруктураКолонки.Заголовок		= ЭлементМассива.Заголовок;
			СтруктураКолонки.ТипЗначения	= ЭлементМассива.ТипЗначения;
			СтруктураКолонки.ИмяЭлемента	= НайденныйЭлемент;
		
			СоответствиеКолонокТаблицы.Вставить(НайденныйЭлемент, СтруктураКолонки)
		КонецЦикла;
		
		СтруктураТаблицы = ПолучитьСтруктуруОписанияТаблицы();
		СтруктураТаблицы.ТаблицаИмяЭлемента	= ТаблицаИмяЭлемента;
		СтруктураТаблицы.ТаблицаПутьКДанным	= ТаблицаПутьКДанным;
		СтруктураТаблицы.ОписаниеКолонок	= СоответствиеКолонокТаблицы;
		
		ОписаниеТаблиц.Вставить(ТаблицаИмяЭлемента, СтруктураТаблицы);

		//Добавим элементы формы для отображения суммы выделенных строк
		ИмяРодителяГруппыРасчета = Настройки.НастройкиРазмещения.Получить(ТаблицаИмяЭлемента);
		Если ИмяРодителяГруппыРасчета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РодительГруппыРасчета = ЭлементыФормы[ИмяРодителяГруппыРасчета];
		Если ТипЗнч(РодительГруппыРасчета) <> Тип("ГруппаФормы") Тогда
			Продолжить;
		КонецЕсли; 
		
		ТаблицаДоступна = ЭлементТаблица.Видимость И ЭлементТаблица.Доступность;
		Если НЕ ТаблицаДоступна Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизитаСуммаСтрок = ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаСуммыВыделенныхСтрок(ТаблицаИмяЭлемента);
		РеквизитСуммаСтрок = Новый РеквизитФормы(ИмяРеквизитаСуммаСтрок, ОбщегоНазначения.ОписаниеТипаЧисло(20,4));

		МассивДобавляемыхРеквизитов.Добавить(РеквизитСуммаСтрок);

		ДобавитьРеквизитРасчетаСуммыСтрок(Форма, ТаблицаИмяЭлемента, РодительГруппыРасчета);
		
	КонецЦикла;

	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	
	Для Каждого ОписаниеТаблицы Из ОписаниеТаблиц Цикл
		ДобавитьРеквизитРасчетаСуммыСтрок(Форма, ОписаниеТаблицы.Ключ, РодительГруппыРасчета);
	КонецЦикла;
	
	Форма[ИмяРеквизитаОписанияТаблицФормы] = Новый ФиксированноеСоответствие(ОписаниеТаблиц);
	
КонецПроцедуры

// Функция Элемент родитель в который будет добавлена команда группового редактирования строк
// Соответствие Элементов Ключ: Имя элемента с типом "ТаблицаФормы", Значение: Имя элемента с типом "ФормаКлиентскогоПриложения", "ГруппаФормы" или "ГруппаКоманд" (в котором создаем команду вызова группового редактирования)
//ПРИМЕР:
//Соответствие.Вставить("ТаблицаОбъекта"	, "ТаблицаГруппаКоманд1");
//Соответствие.Вставить("Дерево6"			, "ДеревоГруппа2");
//
Функция НастройкиИнициализации() Экспорт
                 
	Настройки = Новый Структура;
	
	Настройки.Вставить("ДоступныеТаблицы"	, Новый Массив);
	Настройки.Вставить("ДоступныеКолонки"	, Новый Массив);
	Настройки.Вставить("НедоступныеКолонки"	, Новый Массив);   
	Настройки.Вставить("НастройкиРазмещения", Новый Соответствие);   

	Возврат Настройки;
	
КонецФункции
	
// Элемент родитель в который будет добавлена элемент для отображения суммы выделенных строк
// Соответствие Элементов Ключ: Имя элемента с типом "ТаблицаФормы", Значение: Имя элемента с типом "ГруппаФормы" (в котором создаем элемент для отображения суммы выделенных строк)
//ПРИМЕР:
//Соответствие.Вставить("ТаблицаОбъекта"	, "ТаблицаГруппа1");
//Соответствие.Вставить("Дерево6"			, "ДеревоГруппа2");
//
Функция НастройкиИнициализацииСуммыВыделенныхСтрок() Экспорт
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("ДоступныеТаблицы"	, Новый Массив);
	Настройки.Вставить("ДоступныеКолонки"	, Новый Массив);
	Настройки.Вставить("НедоступныеКолонки"	, Новый Массив);   
	Настройки.Вставить("НастройкиРазмещения", Новый Соответствие);   

	Возврат Настройки;
	
КонецФункции

Процедура ПриЧтенииНаСервере(Форма, знач НастройкиИнициализации) Экспорт
		
	СформироватьОписаниеТабличныхЧастейФормы(Форма, НастройкиИнициализации);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСоответствиеСуществующихЭлементовФормы(знач Форма, знач ПутьКРодительскомуРеквизиту = "")
	
	МассивСуществующихРеквизитов = Форма.ПолучитьРеквизиты(ПутьКРодительскомуРеквизиту);    
	
	СуществующиеРеквизиты = Новый Соответствие;
	
	Для Каждого Реквизит Из МассивСуществующихРеквизитов Цикл
		СуществующиеРеквизиты.Вставить(Реквизит.Имя, Реквизит);		
	КонецЦикла;

	Возврат СуществующиеРеквизиты;
	
КонецФункции

Функция ПолучитьТаблицуРодительКолонки(знач ЭлементКолонка)
	Если ТипЗнч(ЭлементКолонка) = Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат Неопределено;		
	КонецЕсли;               

	Если ТипЗнч(ЭлементКолонка.Родитель) = Тип("ТаблицаФормы") Тогда
		Возврат ЭлементКолонка.Родитель;
	Иначе
		Возврат ПолучитьТаблицуРодительКолонки(ЭлементКолонка.Родитель)		
	КонецЕсли;                         
			
КонецФункции

Функция ПолучитьСтруктуруОписанияТаблицы()

	СтруктураТаблицы = Новый Структура;
	СтруктураТаблицы.Вставить("ТаблицаИмяЭлемента"	, "");
	СтруктураТаблицы.Вставить("ТаблицаПутьКДанным"	, "");
	СтруктураТаблицы.Вставить("ОписаниеКолонок"		, Новый Соответствие);

	Возврат СтруктураТаблицы;
	
КонецФункции

Функция ПолучитьСтруктуруОписанияКолонки()
	
	СтруктураКолонки = Новый Структура;
	СтруктураКолонки.Вставить("Имя" 		, "");	
	СтруктураКолонки.Вставить("Путь"		, "");
	СтруктураКолонки.Вставить("Заголовок"   , "");
	СтруктураКолонки.Вставить("ТипЗначения" , Неопределено);
	СтруктураКолонки.Вставить("ИмяЭлемента" , "");  
	
	Возврат СтруктураКолонки;
	
КонецФункции

// Создает служебный реквизит для хранения описания таблиц формы
//
// Параметры:
//	Форма - УправляемаяФорма - форма по данным которой надо получить значения реквизитов
//
//Заполнение реквизита "_Служебный_ОписаниеТаблицДляГрупповогоРедактирования_" (см. ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаНастроекГрупповогоРедактирования()) 
//	- ФиксированноеСоответствие - Ключ: ТаблицаИмяЭлемента формы, Значение: Структура
//СтруктураТаблиц.ТаблицаИмяЭлемента- Строка
//СтруктураТаблиц.ПутьКДанным		- Строка
//СтруктураТаблиц.ОписаниеКолонок	- Соответствие - Описание доступных колонок таблицы (Имя, Заголовок, Путь, ТипЗначения)
//
Процедура ДобавитьРеквизитХраненияОписанияТаблицФормы(Форма, МассивДобавляемыхРеквизитов, ИмяРеквизитаОписанияТаблицФормы)
	
	РеквизитСуществует = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизитаОписанияТаблицФормы);
	
	Если РеквизитСуществует Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитОписаниеТаблицДляГрупповогоРедактирования = Новый РеквизитФормы(ИмяРеквизитаОписанияТаблицФормы, Новый ОписаниеТипов("Неопределено"));
	
	МассивДобавляемыхРеквизитов.Добавить(РеквизитОписаниеТаблицДляГрупповогоРедактирования);
	
КонецПроцедуры

Процедура ДобавитьКнопкуВКоманднуюПанельТаблицы(Форма, ЭлементТаблица, НастройкиРазмещения)
	
	ТаблицаИмяЭлемента = ЭлементТаблица.Имя;
	ИмяКоманды = ТаблицаИмяЭлемента + "_ГрупповоеРедактированиеСтрок_";
	
	КомандаТаблицы = Форма.Команды.Найти(ИмяКоманды);
	Если ТипЗнч(КомандаТаблицы) <> Тип("КомандаФормы") Тогда
		КомандаТаблицы = Форма.Команды.Добавить(ИмяКоманды);
		
		КомандаТаблицы.Заголовок  	= "Групповое редактирование строк"; 	
		КомандаТаблицы.Подсказка  	= "Групповое редактирование выделенных строк таблицы";
		КомандаТаблицы.Действие   	= "Подключаемый_ГрупповоеРедактированиеСтрок"; 
		КомандаТаблицы.Картинка		= БиблиотекаКартинок.ТабличныйДокументТолькоПросмотр;
		КомандаТаблицы.Отображение	= ОтображениеКнопки.Картинка;
	КонецЕсли; 
	
	ИмяКнопки = ТаблицаИмяЭлемента + "_ГрупповоеРедактированиеСтрок_";
	
	КнопкаТаблицы = Форма.Элементы.Найти(ИмяКнопки);
	Если ТипЗнч(КнопкаТаблицы) <> Тип("КнопкаФормы") Тогда 
		ИмяРодителяКнопки = НастройкиРазмещения.Получить(ТаблицаИмяЭлемента);
		Если ИмяРодителяКнопки <> Неопределено Тогда
			РодительКнопки = Форма.Найти(ИмяРодителяКнопки); 
			Если ТипЗнч(РодительКнопки) = Тип("КоманднаяПанель")
				ИЛИ ТипЗнч(РодительКнопки) = Тип("КоманднаяПанельФормы")
				ИЛИ ТипЗнч(РодительКнопки) = Тип("ГруппаКнопокФормы") Тогда
				КнопкаТаблицы = Форма.Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), ЭлементТаблица.КоманднаяПанель);
				
				КнопкаТаблицы.Вид    		= ВидКнопкиФормы.КнопкаКоманднойПанели;
				КнопкаТаблицы.Заголовок  	= "Групповое редактирование строк";
				КнопкаТаблицы.ИмяКоманды 	= ИмяКоманды;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 

	КнопкаТаблицы.Видимость 	= ЭлементТаблица.Видимость;
	КнопкаТаблицы.Доступность	= ЭлементТаблица.Доступность И НЕ ЭлементТаблица.ТолькоПросмотр;
	
КонецПроцедуры

Процедура ДобавитьРеквизитРасчетаСуммыСтрок(Форма, ТаблицаИмяЭлемента, РодительГруппыРасчета)
	
	СуществующиеРеквизитыФормы = Форма.ПолучитьРеквизиты(); 
	
	ИмяРеквизитаСуммаСтрок = ГрупповоеРедактированиеТабличныхЧастейКлиентСервер.ИмяРеквизитаСуммыВыделенныхСтрок(ТаблицаИмяЭлемента);

	РеквизитСуществует = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизитаСуммаСтрок);
	
	Если РеквизитСуществует Тогда
		Возврат;
	КонецЕсли;

	РеквизитСуммаСтрок = Новый РеквизитФормы(ИмяРеквизитаСуммаСтрок, ОбщегоНазначения.ОписаниеТипаЧисло(20,4));
	
	//МассивДобавить = Новый Массив;
	//МассивДобавить.Добавить(РеквизитСуммаСтрок);
	//
	//Форма.ИзменитьРеквизиты(МассивДобавить);
	
	ИмяГруппы = ТаблицаИмяЭлемента + "_ГруппаВыделенныхСтрок";
	ГруппаСуммы = Форма.Элементы.Найти(ИмяГруппы);
	
	Если ТипЗнч(ГруппаСуммы) <> Тип("ГруппаФормы") Тогда
		ГруппаСуммы = Форма.Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), РодительГруппыРасчета);//, ЭлементТаблица);
		ГруппаСуммы.Заголовок  				= "Сумма выделенных строк"; 	
		ГруппаСуммы.Вид						= ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаСуммы.Группировка 			= ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаСуммы.ОтображатьЗаголовок 	= Ложь;
		ГруппаСуммы.РастягиватьПоВертикали 	= Ложь;
		ГруппаСуммы.ЦветФона				= ЦветаСтиля.ФонУправляющегоПоля;
	КонецЕсли;
	
	ИмяДекорации = ТаблицаИмяЭлемента + "_ДекорацияРасчетСуммыСтрок_";
	ДекорацияСумма = Форма.Элементы.Найти(ИмяДекорации);
	
	Если ТипЗнч(ДекорацияСумма) <> Тип("ДекорацияФормы") Тогда
		ДекорацияСумма = Форма.Элементы.Добавить(ИмяДекорации, Тип("ДекорацияФормы"), ГруппаСуммы);
		ДекорацияСумма.Заголовок  	= "Сумма выделенных строк"; 
		ДекорацияСумма.Вид			= ВидДекорацииФормы.Картинка;
		ДекорацияСумма.Картинка		= БиблиотекаКартинок.Сумма;
		ДекорацияСумма.Высота		= 1;
		ДекорацияСумма.Ширина		= 2;
	КонецЕсли;
	
	ПолеСуммы = Форма.Элементы.Найти(ИмяРеквизитаСуммаСтрок);
	
	Если ТипЗнч(ПолеСуммы) <> Тип("ПолеФормы") Тогда
		ПолеСуммы = Форма.Элементы.Добавить(ИмяРеквизитаСуммаСтрок, Тип("ПолеФормы"), ГруппаСуммы);//, ЭлементТаблица);
		ПолеСуммы.ОтображениеПодсказки  	= ОтображениеПодсказки.Кнопка;
		ПолеСуммы.ПоложениеЗаголовка		= ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеСуммы.Подсказка  				= "Сумма выделенных строк текущей колонки таблицы. Не работает при выделении всех строк комбинацией клавиш Ctrl+A";
		ПолеСуммы.ПутьКДанным  				= ИмяРеквизитаСуммаСтрок;
		ПолеСуммы.Вид						= ВидПоляФормы.ПолеВвода;
		ПолеСуммы.ТолькоПросмотр 			= Истина;
		ПолеСуммы.Ширина					= 20;
	КонецЕсли;
	
КонецПроцедуры

// Получаем колонки таблиц формы, исключаем нередактируемые колонки, формируем соответствие доступных колонок с их описанием.
// В соответствие попадают в том числе невидимые и недоступные колонки, так как эти свойства могут измениться.
// НедоступныеКолонки - Имена колонок реквизита Таблицы
//Исключаем колонки указанные в параметре НедоступныеКолонки, это может быть колонка или группа колонок
Процедура ПроверитьКолонкуТаблицы(Форма, ТаблицаПутьКДанным, ЭлементКолонка, СоответствиеКолонокНаФорме, ДоступныеКолонки, НедоступныеКолонки, ЕстьДоступныеКолонки, ИгнорироватьСвойстваДоступности = Ложь)
	
	Если ТипЗнч(ЭлементКолонка) = Тип("ПолеФормы") Тогда
		
		// Отсекаем колонки, которые полученые через точку: пример Дерево.Колонка.Значение, исключение составляют таблицы находящиеся в Объекте
		ИмяКолонки = СтрЗаменить(ЭлементКолонка.ПутьКДанным, ТаблицаПутьКДанным + ".","");
		Если СтрЧислоВхождений(ИмяКолонки,".") = 0 И НедоступныеКолонки.Найти(ЭлементКолонка.Имя) = Неопределено Тогда
			
			СоответствиеКолонокНаФорме.Вставить(ИмяКолонки, ЭлементКолонка.Имя);
			
			ДоступностьОперации = ?(ИгнорироватьСвойстваДоступности, Истина, ЭлементКолонка.Доступность И НЕ ЭлементКолонка.ТолькоПросмотр); 
			Если ЭлементКолонка.Видимость И ДоступностьОперации Тогда
				ЕстьДоступныеКолонки = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЭлементКолонка) = Тип("ГруппаФормы") Тогда  
		
		Если НедоступныеКолонки.Найти(ЭлементКолонка.Имя) <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого ПодчиненныйЭлемент Из ЭлементКолонка.ПодчиненныеЭлементы Цикл
			ПроверитьКолонкуТаблицы(Форма, ТаблицаПутьКДанным, ПодчиненныйЭлемент, СоответствиеКолонокНаФорме, ДоступныеКолонки, НедоступныеКолонки, ЕстьДоступныеКолонки, ИгнорироватьСвойстваДоступности);
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры // ПроверитьКолонкуТаблицыНаСервере()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КолонкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ЗаполнениеТаблицыЗначенийНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеТаблицыЗначенийНаСервере(ВыбранноеЗначение, ЗначениеВТекущейЯчейке = Неопределено)      
	
	Если ЗначениеВТекущейЯчейке = Неопределено Тогда
		ЗначенияСтрокКолонки.Очистить();
	КонецЕсли;	    
	
	Если Колонка <> "" Тогда
		ЗначениеДляЗаполнения = ЗначениеВТекущейЯчейке;
	КонецЕсли;

	ТаблицаСтрок = РеквизитФормыВЗначение("ЗначенияСтрокКолонки");
	
	СтрокаДанныхДляНастройки = Неопределено;
	
	Для Каждого СтрокаМассива Из Параметры.МассивДанныхТаблицы Цикл
		Если СтрокаМассива.Имя <> ВыбранноеЗначение Тогда
			Продолжить;
		КонецЕсли; 
		
		Колонка = СтрокаМассива.Имя;

		Для Каждого Значение Из СтрокаМассива.Значения Цикл
			НоваяСтрока = ТаблицаСтрок.Добавить();
			НоваяСтрока.Значение 				= ?(ЗначениеЗаполнено(Значение.Значение), Значение.Значение, "<Пустое значение>");
			НоваяСтрока.ИндексВыделеннойСтроки 	= Значение.ИндексВыделеннойСтроки;
		КонецЦикла;
		
		СтрокаДанныхДляНастройки = СтрокаМассива;
		
		Прервать;
		
	КонецЦикла;                                  
	
	НастройкаЭлементаПоВыбраннойКолонке(СтрокаДанныхДляНастройки);
	
	ЗначенияСтрокКолонки.Загрузить(ТаблицаСтрок);  
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияСтрокКолонкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ЗначениеДляЗаполнения = ?(ТекущиеДанные.Значение = "<Пустое значение>", Элементы.ЗначениеДляЗаполнения.ОграничениеТипа.ПривестиЗначение(), ТекущиеДанные.Значение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("МассивДанныхТаблицы") Тогда  
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Обработка не предназначена для непосредственного открытия'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.МассивДанныхТаблицы) <> Тип("Массив") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущаяВыделеннаяКолонка = "";
	ЗначениеВТекущейЯчейке = Неопределено;
	Если Параметры.Свойство("ДанныеТекущейСтроки") И Параметры.ДанныеТекущейСтроки.Свойство("ЗначениеВТекущейЯчейке") Тогда 
		ЗначениеВТекущейЯчейке 		= Параметры.ДанныеТекущейСтроки.ЗначениеВТекущейЯчейке;
		ТекущаяВыделеннаяКолонка 	= Параметры.ДанныеТекущейСтроки.ТекущаяВыделеннаяКолонка;
	КонецЕсли;
	
	Для Каждого СтрокаМассива Из Параметры.МассивДанныхТаблицы Цикл
		Элементы.Колонка.СписокВыбора.Добавить(СтрокаМассива.Имя, СтрокаМассива.Заголовок);
		Если ТекущаяВыделеннаяКолонка = СтрокаМассива.Имя Тогда
			Колонка = СтрокаМассива.Имя;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнениеТаблицыЗначенийНаСервере(ТекущаяВыделеннаяКолонка, ЗначениеВТекущейЯчейке);	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВвестиЗначениеЗаполнения(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиЗначениеЗаполненияЗавершение", ЭтаФорма, Новый Структура); 
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Вы действительно хотите изменить значения в выделенных строках?'"), РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ВвестиЗначениеЗаполненияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	СтруктураВозврат = Новый Структура;
	СтруктураВозврат.Вставить("ИмяСтруктуры",		 	"ГрупповоеРедактированиеСтрокТаблицы");
	СтруктураВозврат.Вставить("ЗначениеДляЗаполнения", 	ЗначениеДляЗаполнения);
	СтруктураВозврат.Вставить("Колонка", 				Колонка);
	
	МассивВыделенныхСтрок = новый Массив;
	Для Каждого Строка Из ЗначенияСтрокКолонки Цикл
		МассивВыделенныхСтрок.Добавить(Строка.Индексвыделеннойстроки);
	КонецЦикла;
	СтруктураВозврат.Вставить("МассивВыделенныхСтрок",		МассивВыделенныхСтрок);
	СтруктураВозврат.Вставить("ПутьКДаннымТекущейТаблицы",	Параметры.ДанныеТекущейСтроки.ПутьКДаннымТекущейТаблицы);
	
	Оповестить("ГрупповоеРедактированиеСтрокТаблицы", СтруктураВозврат,);
	//ОповеститьОВыборе(СтруктураВозврат);     
	ЭтотОбъект.Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастройкаЭлементаПоВыбраннойКолонке(знач СтрокаНастройкиЭлемента)
	
	// Установим тип значения
	Если СтрокаНастройкиЭлемента = Неопределено Тогда
		Возврат;		
	КонецЕсли;  

	Элементы.ЗначениеДляЗаполнения.ОграничениеТипа = СтрокаНастройкиЭлемента.ТипЗначения;
	Элементы.ЗначениеДляЗаполнения.ДоступныеТипы = СтрокаНастройкиЭлемента.ТипЗначения;
	
	// Установим параметры выбора
	ПараметрыВыбораЗначения = СтрокаНастройкиЭлемента.ПараметрыВыбора;

	Если ТипЗнч(ПараметрыВыбораЗначения) <> Тип("Соответствие") Тогда
		Возврат;		
	КонецЕсли;  

	МассивПараметровВыбора = Новый Массив;
	
	Для Каждого КлючИЗначение Из ПараметрыВыбораЗначения Цикл	
		ПараметрВыбора = Новый ПараметрВыбора(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		МассивПараметровВыбора.Добавить(ПараметрВыбора);
	КонецЦикла; 
	
	Элементы.ЗначениеДляЗаполнения.ПараметрыВыбора = новый ФиксированныйМассив(МассивПараметровВыбора);
	
	// Установим другие ограничения
	ЗаполнитьЗначенияСвойств(Элементы.ЗначениеДляЗаполнения, СтрокаНастройкиЭлемента.ДополнительныеСвойства);
	ЗаполнитьЗначенияСвойств(Элементы.ЗначенияСтрокКолонкиЗначение, СтрокаНастройкиЭлемента.ДополнительныеСвойства);
КонецПроцедуры


#КонецОбласти

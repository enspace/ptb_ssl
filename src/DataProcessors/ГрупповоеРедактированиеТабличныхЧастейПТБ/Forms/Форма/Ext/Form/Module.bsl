
#Область УправлениеФормой

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, ИмяРеквизита)
	
	Если НЕ Обработано.Найти(ИмяРеквизита) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Обработано.Добавить(ИмяРеквизита);

	Элементы = Форма.Элементы;
	
	#Область НаборыЭлементов
	
	Если ИмяРеквизита = "РеквизитыПереносЗначений" Тогда
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "Колонка");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ПереносЗначенийНа");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ЗначениеДляЗаполнения");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ФормаВвестиЗначениеЗаполнения");
	КонецЕсли;
	
	#КонецОбласти
	
	#Область Элементы
	
	Если ИмяРеквизита = "Колонка" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"Колонка", "Заголовок", НСтр("ru='Перенос значений с'"));
	КонецЕсли;
	
	Если ИмяРеквизита = "Колонка" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"Колонка", "Подсказка", НСтр("ru='Выберите колонку для переноса значений'"));
	КонецЕсли;
	
	Если ИмяРеквизита = "ПереносЗначенийНа" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ПереносЗначенийНа", "Видимость", Форма.РежимПереноса);
	КонецЕсли;
	
	Если ИмяРеквизита = "ЗначениеДляЗаполнения" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ЗначениеДляЗаполнения", "Видимость", НЕ Форма.РежимПереноса);
	КонецЕсли;
	
	Если ИмяРеквизита = "ФормаВвестиЗначениеЗаполнения" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ФормаВвестиЗначениеЗаполнения", "Заголовок", НСтр("ru='Перенести значения в строках'"));
	КонецЕсли;
	
	#КонецОбласти
			
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформление(Форма, ИменаРеквизитов = "")

	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		Если ПустаяСтрока(ИменаРеквизитов) Тогда
			МассивИмен = Новый Массив;
			МассивИмен.Добавить("");
		Иначе
			МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаРеквизитов, ",");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Массив") Тогда
		МассивИмен = ОбщегоНазначенияПТБКлиентСервер.СкопироватьРекурсивно(ИменаРеквизитов);
	Иначе
		Возврат;
	КонецЕсли;
	
	Обработано = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИмен Цикл
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, СокрЛП(ИмяРеквизита));
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КолонкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ЗаполнениеТаблицыЗначенийНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеТаблицыЗначенийНаСервере(ВыбранноеЗначение, ЗначениеВТекущейЯчейке = Неопределено)      
	
	Если ЗначениеВТекущейЯчейке = Неопределено Тогда
		ЭтотОбъект.ЗначенияСтрокКолонки.Очистить();
	КонецЕсли;	    
	
	Если ЭтотОбъект.Колонка <> "" Тогда
		ЭтотОбъект.ЗначениеДляЗаполнения = ЗначениеВТекущейЯчейке;
	КонецЕсли;

	ТаблицаСтрок = РеквизитФормыВЗначение("ЗначенияСтрокКолонки");
	
	СтрокаДанныхДляНастройки = Неопределено;
	
	Для Каждого СтрокаМассива Из Параметры.МассивДанныхТаблицы Цикл
		Если СтрокаМассива.Имя <> ВыбранноеЗначение Тогда
			Продолжить;
		КонецЕсли; 
		
		ЭтотОбъект.Колонка = СтрокаМассива.Имя;

		Для Каждого Значение Из СтрокаМассива.Значения Цикл
			НоваяСтрока = ТаблицаСтрок.Добавить();
			НоваяСтрока.Значение 				= ?(ЗначениеЗаполнено(Значение.Значение), Значение.Значение, "<Пустое значение>");
			НоваяСтрока.ИндексВыделеннойСтроки 	= Значение.ИндексВыделеннойСтроки;
		КонецЦикла;
		
		СтрокаДанныхДляНастройки = СтрокаМассива;
		
		Прервать;
		
	КонецЦикла;                                  
	
	Если НЕ ЭтотОбъект.РежимПереноса Тогда
		НастройкаЭлементаПоВыбраннойКолонке(СтрокаДанныхДляНастройки);
	КонецЕсли;
		
	ЭтотОбъект.ЗначенияСтрокКолонки.Загрузить(ТаблицаСтрок);  
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияСтрокКолонкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ЭтотОбъект.ЗначениеДляЗаполнения = ?(ТекущиеДанные.Значение = "<Пустое значение>", Элементы.ЗначениеДляЗаполнения.ОграничениеТипа.ПривестиЗначение(), ТекущиеДанные.Значение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("МассивДанныхТаблицы") Тогда  
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Обработка не предназначена для непосредственного открытия'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.МассивДанныхТаблицы) <> Тип("Массив") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ОткрытьДляПереноса") Тогда  
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Если НЕ Параметры.Свойство("Источник") Тогда
        Отказ = Истина;
        Возврат;
    КонецЕсли;

    ЭтотОбъект.РежимПереноса	= Параметры.ОткрытьДляПереноса;
    ЭтотОбъект.Источник			= Параметры.Источник;
	
	МассивДанных = ОбщегоНазначенияУМТРКлиентСервер.СортироватьМассив(Параметры.МассивДанныхТаблицы, "Порядок");

	ТекущаяВыделеннаяКолонка = "";
	ЗначениеВТекущейЯчейке = Неопределено;
	Если Параметры.Свойство("ДанныеТекущейСтроки") И Параметры.ДанныеТекущейСтроки.Свойство("ЗначениеВТекущейЯчейке") Тогда 
		ЗначениеВТекущейЯчейке 		= Параметры.ДанныеТекущейСтроки.ЗначениеВТекущейЯчейке;
		ТекущаяВыделеннаяКолонка 	= Параметры.ДанныеТекущейСтроки.ТекущаяВыделеннаяКолонка;
	КонецЕсли;
	
	Для Каждого СтрокаМассива Из МассивДанных Цикл
		Элементы.Колонка.СписокВыбора.Добавить(СтрокаМассива.Имя, СтрокаМассива.Заголовок);
		Если ЭтотОбъект.РежимПереноса Тогда
			Элементы.ПереносЗначенийНа.СписокВыбора.Добавить(СтрокаМассива.Имя, СтрокаМассива.Заголовок);	
		КонецЕсли;
		Если ТекущаяВыделеннаяКолонка = СтрокаМассива.Имя Тогда
			Колонка = СтрокаМассива.Имя;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнениеТаблицыЗначенийНаСервере(ТекущаяВыделеннаяКолонка, ЗначениеВТекущейЯчейке);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтотОбъект.РежимПереноса Тогда
		УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыПереносЗначений");	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВвестиЗначениеЗаполнения(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиЗначениеЗаполненияЗавершение", ЭтаФорма, Новый Структура); 
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Вы действительно хотите изменить значения в выделенных строках?'"), РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ВвестиЗначениеЗаполненияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если (ЭтотОбъект.РежимПереноса И ЭтотОбъект.ПереносЗначенийНа = "") ИЛИ ЭтотОбъект.Колонка = "" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru='Необходимо выбрать %1!'"),
			?(ЭтотОбъект.РежимПереноса, "колонки для переноса", "колонку для заполнения")));		
		Возврат;	
	КонецЕсли;
	
	СтруктураВозврат = Новый Структура;
	СтруктураВозврат.Вставить("ИмяСтруктуры",	"ГрупповоеРедактированиеСтрокТаблицы");
	СтруктураВозврат.Вставить("Колонка", 		ЭтотОбъект.Колонка);
	
	Если ЭтотОбъект.РежимПереноса Тогда
		СтруктураВозврат.Вставить("КолонкаПереносНа",		ЭтотОбъект.ПереносЗначенийНа);
	Иначе
		СтруктураВозврат.Вставить("ЗначениеДляЗаполнения", 	ЭтотОбъект.ЗначениеДляЗаполнения);
	КонецЕсли;
	
	МассивВыделенныхСтрок = новый Массив;
	Для Каждого Строка Из ЭтотОбъект.ЗначенияСтрокКолонки Цикл
		МассивВыделенныхСтрок.Добавить(Строка.Индексвыделеннойстроки);
	КонецЦикла;
	СтруктураВозврат.Вставить("МассивВыделенныхСтрок",		МассивВыделенныхСтрок);
	СтруктураВозврат.Вставить("ПутьКДаннымТекущейТаблицы",	Параметры.ДанныеТекущейСтроки.ПутьКДаннымТекущейТаблицы);
	
	Оповестить("ГрупповоеРедактированиеСтрокТаблицы", СтруктураВозврат, ЭтотОбъект.Источник);
	//ОповеститьОВыборе(СтруктураВозврат);     
	ЭтотОбъект.Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастройкаЭлементаПоВыбраннойКолонке(знач СтрокаНастройкиЭлемента)
	
	// Установим тип значения
	Если СтрокаНастройкиЭлемента = Неопределено Тогда
		Возврат;		
	КонецЕсли;  

	Элементы.ЗначениеДляЗаполнения.ОграничениеТипа = СтрокаНастройкиЭлемента.ТипЗначения;
	Элементы.ЗначениеДляЗаполнения.ДоступныеТипы = СтрокаНастройкиЭлемента.ТипЗначения;
	
	// Установим параметры выбора
	ПараметрыВыбораЗначения = СтрокаНастройкиЭлемента.ПараметрыВыбора;

	Если ТипЗнч(ПараметрыВыбораЗначения) <> Тип("Соответствие") Тогда
		Возврат;		
	КонецЕсли;  

	МассивПараметровВыбора = Новый Массив;
	
	Для Каждого КлючИЗначение Из ПараметрыВыбораЗначения Цикл	
		ПараметрВыбора = Новый ПараметрВыбора(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		МассивПараметровВыбора.Добавить(ПараметрВыбора);
	КонецЦикла; 
	
	Элементы.ЗначениеДляЗаполнения.ПараметрыВыбора = новый ФиксированныйМассив(МассивПараметровВыбора);
	
	// Установим другие ограничения
	ЗаполнитьЗначенияСвойств(Элементы.ЗначениеДляЗаполнения, СтрокаНастройкиЭлемента.ДополнительныеСвойства);
	ЗаполнитьЗначенияСвойств(Элементы.ЗначенияСтрокКолонкиЗначение, СтрокаНастройкиЭлемента.ДополнительныеСвойства);
КонецПроцедуры

#КонецОбласти

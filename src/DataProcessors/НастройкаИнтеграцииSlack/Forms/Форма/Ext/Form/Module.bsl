
#Область УправлениеФормой

&НаСервере 
Процедура ИнициализацияФормы()
	Если НЕ ЗначениеЗаполнено(Объект.Пользователь) Тогда
		Объект.Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	НастройкиПользователя = РегистрыСведений.НастройкиИнтеграцииSlack.ПолучитьЗначенияНастроек(Объект.Пользователь);
	
	ЭтотОбъект.КонтрольноеЧисло = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиПользователя, "КонтрольноеЧисло", 0);
	Если ЭтотОбъект.КонтрольноеЧисло < 100000 Тогда
		ЭтотОбъект.КонтрольноеЧисло = ЗаписатьКонтрольноеЧислоНаСервере(Объект.Пользователь);
	КонецЕсли;
	
	ПроверитьИдентификациюПользователяНаСервере(НастройкиПользователя);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, ИмяРеквизита)

	Если НЕ Обработано.Найти(ИмяРеквизита) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Обработано.Добавить(ИмяРеквизита);

	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;
	
	ПользовательИдентифицирован	= ЗначениеЗаполнено(Форма.ИдентификаторПользователя);

	#Область Наборы
	
	Если ИмяРеквизита = "РеквизитыИдентификации" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ГруппаПользовательИдентифицирован");
	КонецЕсли;

	#КонецОбласти
	
	#Область Элементы
	
	Если ИмяРеквизита = "ГруппаПользовательИдентифицирован" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаПользовательИдентифицирован", "Видимость", ПользовательИдентифицирован);
	КонецЕсли;

	#КонецОбласти 
	
	#Область ТабЧасть_Имя
	
	//Если ИмяРеквизита = "ИмяТабличнойЧастиОтветственный" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
	//	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	//		"ИмяТабличнойЧастиОтветственный", "ТолькоПросмотр", ЗначениеЗаполнено(Объект.Ответственный));
	//КонецЕсли;

	#КонецОбласти
	
	#Область Команды
	
	//Если ИмяРеквизита = "КомандаЗаполнить" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
	//	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	//		"ТаблицаФормыЗаполнить", "Видимость", НЕ Объект.Проведен);
	//КонецЕсли;

	#КонецОбласти 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформление(Форма, ИменаРеквизитов = "")

	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		Если ПустаяСтрока(ИменаРеквизитов) Тогда
			МассивИмен = Новый Массив;
			МассивИмен.Добавить("");
		Иначе
			МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаРеквизитов, ",");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Массив") Тогда
		МассивИмен = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ИменаРеквизитов);
	Иначе
		Возврат;
	КонецЕсли;
 
	//Форма.ТолькоПросмотр = (Форма.СостоянияЗаблокировано.Найти(Форма.СведенияОЗаявкеСостояние) <> Неопределено);

	Обработано = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИмен Цикл
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, СокрЛП(ИмяРеквизита));
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьИдентификацию(Команда)
	ПроверитьИдентификациюПользователяНаСервере();
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыИдентификации");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура ПроверитьИдентификациюПользователяНаСервере(знач НастройкиПользователя = Неопределено)
	Если НЕ ТипЗнч(НастройкиПользователя) = Тип("Структура") Тогда
		НастройкиПользователя = РегистрыСведений.НастройкиИнтеграцииSlack.ПолучитьЗначенияНастроек(Объект.Пользователь);
	КонецЕсли;
	
	ЭтотОбъект.ИдентификаторПользователя = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиПользователя, "ПользовательId", "");
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ИдентификаторПользователя) Тогда
		ЭтотОбъект.ТекстКомандыSlack = СтрШаблон("/user checkout %1", Формат(ЭтотОбъект.КонтрольноеЧисло, "ЧДЦ=0; ЧН=0; ЧГ="));
		ТекстЗаголовка = НСтр("ru='Для отмены связи пользователей 1С и Slack, необходимо в Slack выполнить команду'");
	Иначе 
		ЭтотОбъект.ТекстКомандыSlack = СтрШаблон("/user checkin %1", Формат(ЭтотОбъект.КонтрольноеЧисло, "ЧДЦ=0; ЧН=0; ЧГ="));
		ТекстЗаголовка = НСтр("ru='Для связи пользователей 1С и Slack, необходимо в Slack выполнить команду'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ТекстКомандыSlack", "Заголовок", ТекстЗаголовка);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьКонтрольноеЧислоНаСервере(знач ПользовательСсылка)
	Генератор = Новый ГенераторСлучайныхЧисел;
	КонтрольноеЧисло = Генератор.СлучайноеЧисло(100000, 999999);
	
	РегистрыСведений.НастройкиИнтеграцииSlack.УстановитьЗначениеНастройки(ПользовательСсылка,
		Перечисления.ВидыНастроекИнтеграцииSlack.КонтрольноеЧисло,
		КонтрольноеЧисло);
		
	Возврат КонтрольноеЧисло;
КонецФункции

#КонецОбласти
 

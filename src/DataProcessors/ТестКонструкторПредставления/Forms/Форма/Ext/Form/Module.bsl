
&НаКлиенте
Асинх Процедура НастройкиФорматНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Настройки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	КонструкторФормат = Новый КонструкторФорматнойСтроки(ТекущиеДанные.Формат);
	Результат = Ждать КонструкторФормат.ОткрытьАсинх();
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Формат = Результат;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Повтор = 1;
	
	НаборДанных = Новый Массив;
	НаборДанных.Добавить("Список номенклатуры:");
	НаборДанных.Добавить("Номенклатура");
	НаборДанных.Добавить(1);
	НаборДанных.Добавить("см");
	
	Префиксы = Новый Массив;
	Префиксы.Добавить(" ");
	Префиксы.Добавить(" ");
	Префиксы.Добавить(" - ");
	Префиксы.Добавить("");
	Префиксы.Добавить(". стр.");
	
	Форматы = Новый Массив;
	Форматы.Добавить("");
	Форматы.Добавить("");
	Форматы.Добавить("ЧН=0; ЧГ=");
	
	Положения = Новый Массив;
	Положения.Добавить(-1);
	
	Для Индекс = 1 По 10 Цикл
		СтрокаНастройки = Объект.Настройки.Добавить();
		СтрокаНастройки.ИмяПоля		= "Реквизит." + Формат(Индекс, "ЧН=0; ЧГ=");
		СтрокаНастройки.Значение	= ?(Индекс <= НаборДанных.Количество(), НаборДанных[Индекс - 1], Строка(Индекс));
		СтрокаНастройки.Префикс		= ?(Индекс <= Префиксы.Количество(), Префиксы[Индекс - 1], ", стр.");
		СтрокаНастройки.Формат		= ?(Индекс <= Форматы.Количество(), Форматы[Индекс - 1], "");
		СтрокаНастройки.Положение	= ?(Индекс = 1, -1, ?(Индекс = 10, 1, 0));
		
		Если Индекс = 10 Тогда
			СтрокаНастройки.Префикс		= ". ";
			СтрокаНастройки.Значение	= "Строка окончена";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставление(Команда)
	
	НастройкиПредставления	= Новый Соответствие;
	ДанныеПредставления		= Новый Соответствие;
	
	СтрокаДанных	= Новый Соответствие;
	СписокПолей		= Новый Массив;
	
	Для Каждого СтрокаТаблицы Из Объект.Настройки Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		СписокПолей.Добавить(СтрокаТаблицы.ИмяПоля);
		
		СтрокаДанных.Вставить(СтрокаТаблицы.ИмяПоля, СтрокаТаблицы.Значение);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Префикс) Тогда
			КонструкторПредставленияКлиентСервер.УстановитьПрефикс(НастройкиПредставления,
				СтрокаТаблицы.ИмяПоля, СтрокаТаблицы.Префикс);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Положение) Тогда
			КонструкторПредставленияКлиентСервер.УстановитьПоложение(НастройкиПредставления,
				СтрокаТаблицы.ИмяПоля, СтрокаТаблицы.Положение);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Формат) Тогда
			КонструкторПредставленияКлиентСервер.УстановитьФормат(НастройкиПредставления,
				СтрокаТаблицы.ИмяПоля, СтрокаТаблицы.Формат);
		КонецЕсли;
	КонецЦикла;
	
	НаборДанных = Новый Массив;
	НаборДанных.Добавить(СтрокаДанных);
	
	Для Индекс = 2 По Объект.Повтор Цикл
		НаборДанных.Добавить(СтрокаДанных);
	КонецЦикла;
	
	Объект.Результат = КонструкторПредставленияКлиентСервер.ПодготовитьПредставление(НаборДанных,
		СписокПолей,
		НастройкиПредставления);
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьТест(Команда)
	ЭтотОбъект.РезультатТеста = "";
	ЭтотОбъект.СтрокаJSON_1 = "";
	ЭтотОбъект.СтрокаJSON_2 = "";
	
	Массив = Новый Массив;
	Массив.Добавить("ЭтоМассив");
	ФМассив = Новый ФиксированныйМассив(Массив);
	
	Структура = Новый Структура("ЭтоСтруктура", Истина);
	ФСтруктура = Новый ФиксированнаяСтруктура(Структура);
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("ЭтоСоответствие", Истина);
	ФСоответствие = Новый ФиксированноеСоответствие(Соответствие);	
	
	Контроль = Новый Структура;
	Контроль.Вставить("Число"	, 1000);
	Контроль.Вставить("Строка"	, "Тест");
	Контроль.Вставить("Булево"	, Истина);
	Контроль.Вставить("Дата"	, ОбщегоНазначенияКлиент.ДатаСеанса());
	
	Контроль.Вставить("Массив"						, Массив);
	Контроль.Вставить("ФиксированныйМассив"			, ФМассив);
	Контроль.Вставить("Структура"					, Структура);
	Контроль.Вставить("ФиксированнаяСтруктура"		, ФСтруктура);
	Контроль.Вставить("Соответствие"				, Соответствие);
	Контроль.Вставить("ФиксированноеСоответствие"	, ФСоответствие);
	
	ДополнитьКонтрольСсылкамиНЕаСервере(Контроль);
	
	СериализацияНаКлиенте(Контроль);
	СериализацияНаСервере(Контроль);
КонецПроцедуры

&НаКлиенте 
Процедура СериализацияНаКлиенте(знач Контроль)
	ЭтотОбъект.СтрокаJSON_1 = ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON(Контроль);
	
	ЗаписатьРезультат(РезультатТеста, НСтр("ru='На клиенте'"), НСтр("ru='простая запись - Успешно'"));
	
	ДополнительноСериализация(Контроль, ЭтотОбъект.РезультатТеста);
КонецПроцедуры
	
&НаСервере 
Процедура СериализацияНаСервере(знач Контроль)
	ЭтотОбъект.СтрокаJSON_2 = ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON(Контроль);
	
	ЗаписатьРезультат(РезультатТеста, НСтр("ru='На сервере'"), НСтр("ru='простая запись - Успешно'"));
	
	ДополнительноСериализация(Контроль, ЭтотОбъект.РезультатТеста);	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ДополнительноСериализация(знач Контроль, РезультатТеста)
	#Если Сервер Тогда
	    ТекстДиректива = НСтр("ru='На сервере'");
		КопияТест = ОбщегоНазначения.СкопироватьРекурсивно(Контроль);
	#Иначе
		ТекстДиректива = НСтр("ru='На клиенте'");
		КопияТест = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Контроль);
	#КонецЕсли
	
	Параметры = ИнтернетСервисыКлиентСервер.ПараметрыЗначениеВСтрокуJSON();
	Параметры.РасширеннаяСериализация = Ложь;
	
	// исключение
	Параметры.ИспользоватьИсключение = Истина;
	Попытка
		ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON(КопияТест,, Параметры);
		ЗаписатьРезультат(РезультатТеста, ТекстДиректива, НСтр("ru='запись ссылки, исключение - Ошибка'"));
	Исключение
		ЗаписатьРезультат(РезультатТеста, ТекстДиректива, НСтр("ru='запись ссылки, исключение - Успешно'"));
	КонецПопытки;
	Параметры.ИспользоватьИсключение = Ложь;
	
	// отказ при ошибке
	Параметры.ОтказПриОшибкеСериализации = Истина;
	КонтрольнаяСтрока	= ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON(КопияТест,, Параметры);
	ЗначениеИзСтроки	= ИнтернетСервисыКлиентСервер.ЗначениеИзСтрокиJSON(КонтрольнаяСтрока, Ложь);
	ЗначениеСсылки		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначениеИзСтроки, "Справочник", "");
	Если НЕ ЗначениеЗаполнено(ЗначениеСсылки) Тогда
		ЗаписатьРезультат(РезультатТеста, ТекстДиректива, НСтр("ru='запись ссылки, отказ - Успешно'"));
	Иначе
		ЗаписатьРезультат(РезультатТеста, ТекстДиректива, НСтр("ru='запись ссылки, отказ - Ошибка'"));
	КонецЕсли;
	Параметры.ОтказПриОшибкеСериализации = Ложь;
	
	// текст ошибки сериализации
	КонтрольнаяСтрока	= ИнтернетСервисыКлиентСервер.ЗначениеВСтрокуJSON(КопияТест,, Параметры);
	ЗначениеИзСтроки	= ИнтернетСервисыКлиентСервер.ЗначениеИзСтрокиJSON(КонтрольнаяСтрока, Ложь);
	ЗначениеСсылки		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначениеИзСтроки, "Справочник", "");
	Если ЗначениеСсылки = Параметры.ТекстПриОшибкиСериалиазации Тогда
		ЗаписатьРезультат(РезультатТеста, ТекстДиректива, НСтр("ru='запись ссылки, текст ошибки - Успешно'"));
	Иначе
		ЗаписатьРезультат(РезультатТеста, ТекстДиректива, НСтр("ru='запись ссылки, текст ошибки - Ошибка'"));
	КонецЕсли;
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста 
Процедура ЗаписатьРезультат(Результат, знач Директива, знач Текст)
	Результат = Результат
		+ ?(НЕ ЗначениеЗаполнено(Результат), "", Символы.ПС)
		+ "- " + Директива + ": " + Текст;
КонецПроцедуры

&НаСервере 
Процедура ДополнитьКонтрольСсылкамиНЕаСервере(Контроль)
	Контроль.Вставить("Перечисление", Перечисления.ВидыНастроекИнтеграцииSlack.ПользовательId);
	Контроль.Вставить("Справочник"	, Справочники.БазыЗнанийConfluence.ОсновнаяБазаЗнаний());
КонецПроцедуры

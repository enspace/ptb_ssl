
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	П1 = НСтр("ru='ВТБ снизил минимальную ставку по кредитам наличными'");
	П2 = НСтр("ru='Сбербанк снизил ставки по ряду кредитов'");
КонецПроцедуры

&НаКлиенте
Процедура ТестСтрокаВBase64(Команда)
	ВывестиСторону(1); 
	СтрокаРезультат = ВыполнитьТест_СтрокаВBase64();
	
	ВывестиСторону(2); 
	ТестСтрокаВBase64НаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТестBase64ВСтроку(Команда)
	ВывестиСторону(1); 
	СтрокаРезультат = ВыполнитьТест_Base64ВСтроку();
	
	ВывестиСторону(2); 
	ТестBase64ВСтрокуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТестСравненияПредложений(Команда)
	Коэффициент = СтроковыеФункцииПТБКлиентСервер.КоэффициентНечеткогоСравненияПредложений(П1, П2);
	ЭтотОбъект.СтрокаРезультат = Строка(Коэффициент);
	
	ВывестиСторону(1);
	ВывестиРезультат(Коэффициент >= 0.25, "ТестСравненияПредложений");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТестСтрокаВBase64НаСервере()
	ВыполнитьТест_СтрокаВBase64();   
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТестBase64ВСтрокуНаСервере()
	ВыполнитьТест_Base64ВСтроку();   
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВыполнитьТест_СтрокаВBase64()
	СтрокаА = "teststring";
	СтрокаРезультат = СтроковыеФункцииПТБКлиентСервер.СтрокаВBase64(СтрокаА);
	
	ДлинаКратна4 = (СтрДлина(СтрокаРезультат) % 4 = 0);
	
	СтрокаПроверки = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(СтрокаРезультат, "=", "Справа");
	
	ЕстьОшибки = Ложь;
	Для Индекс = 1 По СтрДлина(СтрокаПроверки) Цикл
		СимволПроверки = Сред(СтрокаПроверки, Индекс, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЛатиницаВСтроке(СимволПроверки)
			ИЛИ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СимволПроверки)
			ИЛИ СимволПроверки = "+"
			ИЛИ СимволПроверки = "/" Тогда
			
			Продолжить;
		КонецЕсли;
		
		ЕстьОшибки = Истина;
		Прервать;
	КонецЦикла;
	
	Результат = ДлинаКратна4 И НЕ ЕстьОшибки И (СтрокаРезультат = "dGVzdHN0cmluZw==");
	
	ВывестиРезультат(Результат, "СтрокаВBase64");   
	
	Возврат СтрокаРезультат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВыполнитьТест_Base64ВСтроку()
	СтрокаА = "dGVzdHN0cmluZw==";
	СтрокаРезультат = СтроковыеФункцииПТБКлиентСервер.Base64ВСтроку(СтрокаА);
	
	Результат = (СтрокаРезультат = "teststring");
	
	ВывестиРезультат(Результат, "Base64ВСтроку");   
	
	Возврат СтрокаРезультат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура ВывестиРезультат(знач Результат, знач ИмяТеста)
	Текст = ИмяТеста + ":" + ?(Результат, НСтр("ru='выполнен'"), НСтр("ru='провален'"));
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);   
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ВывестиСторону(знач Сторона)
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(?(Сторона = 1, НСтр("ru='На клиенте'"), НСтр("ru='На сервере'"))); 
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ВыполнитьКонтрольРезультатовПроведения(ДокументОбъект, Отказ) Экспорт
	МетаРегистр = ЭтотОбъект.ДополнительныеСвойства.Метаданные;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ЭтотОбъект.ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрОстаткиОстатки.Организация КАК Организация,
	|	РегистрОстаткиОстатки.Пользователь КАК Пользователь,
	|	РегистрОстаткиОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.РегистрОстатки.Остатки(
	|			,
	|			(Организация, Пользователь) В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Организация,
	|					Т.Пользователь
	|				ИЗ
	|					РегистрОстаткиИзменение КАК Т)) КАК РегистрОстаткиОстатки
	|ГДЕ
	|	РегистрОстаткиОстатки.КоличествоОстаток < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрОстаткиИзменение";
	
	ДокументОбъект.ДополнительныеСвойства.РезультатКонтроля.Вставить(МетаРегистр.Имя, Запрос.Выполнить().Выгрузить());
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийМодуля

Процедура ПередЗаписью(Отказ, Замещение)
	
	ПроведениеСерверПТБ.ПередЗаписьюРегистра(ЭтотОбъект, Отказ, Замещение);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	ПравилаПроверки = Новый Массив;
	ПравилаПроверки.Добавить("СУММА(ТаблицаДвижений.Количество) < 0");
	
	ПроведениеСерверПТБ.УстановитьПравилаПроверкиРегистра(ЭтотОбъект, ПравилаПроверки);
	
	ПроведениеСерверПТБ.ПриЗаписиРегистра(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 